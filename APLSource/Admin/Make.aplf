 {r}←{noQLX}Make batchFlag;P;parms;P_;list;F;C;CFG;path;file1;file2;ts1;ts2;versionOfAPL;q;buff;commitFlag;activateFilename
⍝ This function will start two APL sessions in succession in order to open the Tatin
⍝ project and then execute first `MakeClient` (which bumps the build number) and then,
⍝ in the second session, `MakeServer`, which does not bump the version number.
⍝ It also re-compiles the documentation (=creates HTML files from Markdown)
⍝ It cannot be done in one go, and it cannot be done in batch mode from the current session.
⍝ The right argument is expected to be a Boolean that tells whether this functions runs in batch
⍝ mode or not. In batch mode no messages are printed, and no interaction with a user is
⍝ attempted.
⍝ Also, Tatin is not updated in "C:\Users\<⎕AN>\Documents\Dyalog APL-64 19.0 Unicode Files\" then.
⍝ `noQLX` defaults to 0. By passing a 1 no function is called after instantiating Dyalog
⍝ for the Client and the Server; useful for debugging.
 r←⍬
 Assert(⊂batchFlag)∊0 1
 'Currently we build on Windows only, sorry'Assert ##.Registry.IsWindows
 versionOfAPL←'18.2'
 :If ~batchFlag
 :AndIf 0=1 ##.Client.CommTools.YesOrNo'Create Tatin with version ',versionOfAPL,' ?'
     ⎕←'Cancelled by user' ⋄ →0
 :EndIf
 F←##.FilesAndDirs ⋄ C←##.Client ⋄ CFG←##.CiderConfig ⋄ F.APLTreeUtils2←##.APLTreeUtils2
 {}F.Cd CFG.HOME
 0 0⍴{⎕←⍵}⍣(~batchFlag)⊣'   Current dir is now: ',CFG.HOME
 'Check current directory'Assert{'Tatin'≡⊃¯1↑(~⍵∊'/\')⊆⍵}1⊃1 ⎕NPARTS''
 noQLX←{0<⎕NC ⍵:⍎⍵ ⋄ 0}'noQLX'

 0 0⍴{⎕←⍵}⍣(~batchFlag)⊣'   Compiling HTML from the markdown files (documentation)...'
 commitFlag←0
 :If ~batchFlag
 :AndIf (⊂'#.Tatin')∊{⍵[;1]}⎕SE.Cider.ListOpenProjects 0
     buff←1 ⎕SE.APLGit2.Status'#.Tatin'
     :If 0<+/~({⊃,/1↓⎕NPARTS ⍵}¨buff[;2])∊,⊂'History.apla'
     :AndIf 0=1 ##.Client.CommTools.YesOrNo'The working area is dirty - continue anyway?'
         ⎕←'Cancelled by user' ⋄ →0
     :EndIf
     ##.Registry.⎕ED⍠('EditName' 'Allow')⊢'Version'
     commitFlag←0<≢1 ⎕SE.APLGit2.Status'#.Tatin'
 :EndIf
 CompileDocumentation ⍬
 path←##.Client.GetUserDocumentsFolder,'Dyalog APL-64 19.0 Unicode Files\SessionExtensions\CiderTatin\Tatin'
 :If F.IsDir path                                           ⍝ Does a folder Tatin/  live in the version-agnostic folder?
 :AndIf F.IsFile path,'/Tatin.dyalog'                       ⍝ Does the Tatin user command script live there?
     file1←path,'/Tatin.dyalog'
     file2←##.CiderConfig.HOME,'/APLSource/Tatin.dyalog'
 :AndIf ≢/{⊃⎕NGET ⍵ 0}¨file1 file2                          ⍝ Are the two not identical ?
     (ts1 ts2)←{¯1 1 ⎕DT⊂⊃⊃('type' 3)F.Dir ⍵}¨file1 file2
     :If ts2<ts1
         q←'The Tatin user command script in',⎕UCS 13
         q,←path,⎕UCS 13
         q,←' appears to be newer than the one in the project - copy?'
         :If 1 C.YesOrNo q
             file2 ⎕NCOPY⍠('IfExists' 'Replace')⊣file1
         :EndIf
     :Else
         ⍝ We don't need to do anything because it will be copied over later on, if the user wishes so.
     :EndIf
 :EndIf
 'Create!'F.CheckPath'./Dist'
 list←⊃F.Dir'./Dist/*.zip'
 list←('.zip'∘≡¨3⊃¨⎕NPARTS¨list)/list
 list←(∨/¨{∨/¨'Tatin-Client' 'Tatin-Server' 'Tatin-Documentation'⍷¨⊂⍵}¨list)/list
 F.DeleteFile list
 P←##.APLProcess
 parms←P.CreateParms
 parms.Exe←GetAPLVersion versionOfAPL
 parms.CommandLineArgs,←'load="',F.PWD,'\APLSource\Admin\Make_.aplc" '
 parms.CommandLineArgs,←{0=≢v←2 ⎕NQ #'GetEnvironment'⍵:v ⋄ ⍵,'="',v,'"'}'DYALOGSTARTUPSE'
 parms.CommandLineArgs,←' lx="1 #.Make_.Run 1"'
 :If 0  ⍝ Only for analyzing problems:
     parms.CommandLineArgs,←' -exec_setup=0 '
     parms.CommandLineArgs,←' -stop_in_setup=1 '
 :EndIf
 :If noQLX
     parms.CommandLineArgs,←' -x'
 :EndIf
 parms.SessionFile←1
 parms.WindowsStyle←'Normal'
 0 0⍴{⎕←⍵}⍣(~batchFlag)⊣'   Process started for "making" the Tatin Client...'
 P_←⎕NEW P(,⊂parms)
 {⍵.HasExited:shy←0 ⋄ _←⎕DL 0.2 ⋄ ∇ ⍵}P_
 'The process exited with an error code'Assert 0=P_.Proc.ExitCode
 UpdateDocumentation ⍬
 parms←P.CreateParms
 parms.CommandLineArgs←'load="APLSource/Admin/Make_.aplc" '
 parms.CommandLineArgs,←{0=≢v←2 ⎕NQ #'GetEnvironment'⍵:v ⋄ ⍵,'="',v,'"'}'DYALOGSTARTUPSE'
 parms.CommandLineArgs,←' lx="0 #.Make_.Run 2"'
 parms.Exe←GetAPLVersion versionOfAPL
 :If 0          ⍝ Only for analyzing problems:
     parms.CommandLineArgs,←' -exec_setup=0'
     parms.CommandLineArgs,←' -stop_in_setup=1 '
 :EndIf
 :If noQLX      ⍝ Only for analyzing problems:
     parms.CommandLineArgs,←' -x'
 :EndIf
 parms.SessionFile←1
 parms.WindowsStyle←'Normal'
 0 0⍴{⎕←⍵}⍣(~batchFlag)⊣'   Process started for "making" the Tatin Server...'
 P_←⎕NEW P(,⊃parms)
 {⍵.HasExited:shy←0 ⋄ _←⎕DL 0.2 ⋄ ∇ ⍵}P_
 'The process exited with an error code'Assert 0=P_.Proc.ExitCode
 0 0⍴{⎕←⍵}⍣(~batchFlag)⊣'   The final steps, putting it all together...'
 CopyActivateUserCommandIntoProject ⍬
 HandleRunFunction ⍬
 HandleActivateUserCommand ⍬
 UpdateTestServerAssets ⍬
 {}⎕SE.Link.Refresh'#.Tatin'
 :If ~batchFlag
     :If 1 ##.Client.CommTools.YesOrNo'Would you like to install the freshly created new version?'
         ⎕←1 #.Tatin.Admin.InstallTatin 1
     :EndIf
     activateFilename←##.Client.GetMyUCMDsFolder'/activate.dyalog'
     :If 0=⎕NEXISTS activateFilename
     :OrIf './Dist/activate.dyalog'F.YoungerThan activateFilename
         :If 1 ##.Client.CommTools.YesOrNo'Install the ]activate user command?'
             activateFilename ⎕NCOPY⍠('IfExists' 'Replace')⊢'./Dist/activate.dyalog'
         :EndIf
     :EndIf
 :EndIf
 :If #.Tatin.Registry.Version≢⎕SE.Tatin.Version
     ⎕SE.⎕EX¨'Tatin' '_Tatin'
     ⎕←'*** New version ',({(2⊃⍵),' from ',(3⊃⍵)}##.Registry.Version),' created, but version in ⎕SE is not up-to-date anymore, please restart Dyalog'
 :Else
     #.Tatin.Registry ⎕SE.Link.Fix⊃⎕NGET(CFG.HOME,'/APLSource/Registry/Version.aplf')1  ⍝ Because building the Client changes the build number
     0 0⍴{⎕←⍵}⍣(~batchFlag)⊣'*** Done'
 :EndIf
 :If commitFlag
     q←1↓⊃,/(⎕UCS 13),¨↓↑⎕SE.APLGit2.Status'#.Tatin'
     :If 1 ##.Client.CommTools.YesOrNo q,(⎕UCS 13),'---> Commit as "Make"?'
         ⎕SE.APLGit2.Add'#.Tatin'
         buff←'<Make>'⎕SE.APLGit2.Commit'#.Tatin'
     :EndIf
 :EndIf
⍝Done
