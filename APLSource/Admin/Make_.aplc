:Class Make_
⍝ This class is only ever used when Tatin's `Admin.Make` function is called.\\
⍝ It's purpose is to call either `#.Tatin.Admin.MakeClient` or `#.Tatin.Admin.MakeSever`
⍝ (depending on the right argument of the `Run` function) after a new instance of Dyalog
⍝ was fired up by the `Admin.Make` function.
⍝ ⍵ = 1 = Client
⍝ ⍵ = 2 = Server

    ∇ {r}←bumpBuild Run type;home;targetFolder;parms;success;log
    ⍝ Calls either the `#.Tatin.Admin.MakeClient` or the `#.Tatin.Admin.MakeSever` function depending
    ⍝ on the right argument `type`.
    ⍝ * 1 = call `#.Tatin.Admin.MakeClient`
    ⍝ * 2 = call `#.Tatin.Admin.MakeServer`
    ⍝ Otherwise throw an error or ⎕OFF 123 .\\
    ⍝ `bumpBuild` defines whether the build ID is bumped (1) or not (0).\\
      :Access Public Shared
      r←⍬
      home←1⊃1 ⎕NPARTS''
     
      :If type≡1
          targetFolder←home,'/Dist/Client'
          3(⎕NDELETE Attempt)targetFolder
          3(⎕MKDIR Attempt)targetFolder
          parms←⎕SE.Cider.CreateOpenParms ⍬
          parms.folder←'./'
          parms.quietFlag←1
          (success log)←⎕SE.Cider.OpenProject parms
          'Opening the Cider project was not successful'Assert success
          bumpBuild #.Tatin.Admin.MakeClient 1          ⍝ Must be addressed with absolute path!
      :ElseIf type≡2
          targetFolder←home,'/Dist/Server'
          3(⎕NDELETE Attempt)targetFolder
          3(⎕MKDIR Attempt)targetFolder
          parms←⎕SE.Cider.CreateOpenParms ⍬
          parms.folder←'./'
          parms.quietFlag←1
          (success log)←⎕SE.Cider.OpenProject parms
          'Opening the Cider project was not successful'Assert success
          bumpBuild #.Tatin.Admin.MakeServer 1          ⍝ Must be addressed with absolute path!
      :Else
          ∘∘∘ ⍝ Huuh?!
      :EndIf
      ##.⎕EX'Make_'
    ∇

    ⍝ In case an assert is not fulfilled we ⎕OFF with an error code in runtime and crash otherwise:
    Assert←{⍺←'' ⋄ (,1)≡,⍵:r←1 ⋄ 'Development'≡4⊃'#'⎕WG'APLVersion':⍺ ⎕SIGNAL 11 ⋄ errCode←123 ⋄ ⎕OFF errCode}

    ∇ {r}←{x}(f Attempt)y;success;counter
    ⍝ Uses this in case a function might fail on the first attempt. `⎕NDELETE` and `⎕MKDIR` are candidates.\\
    ⍝ We try up to 10 times with an increasingly longer delay between 0.01 and 0.55 seconds.
      r←⍬
      counter←success←0
      :Repeat
          :Trap 19
              :If 0<⎕NC'x'
                  x f y
              :Else
                  f y
              :EndIf
              success←1
          :Else
              ⎕DL 0.05×counter
              counter+←1
          :EndTrap
      :Until success∨10<counter
    ∇

:EndClass
