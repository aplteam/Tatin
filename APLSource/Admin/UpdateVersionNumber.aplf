 {r}←UpdateVersionNumber bumpBuild;name;version;date;build;body;newVersion;wsPath
⍝ `bumpBuild` must be a Boolean, otherwise an error is thrown.
⍝ This is what the function is doing if `bumpBuild` is 1:
⍝ 1. It updates the "date" part of the function ##.Registry.Version
⍝ 2. It bumps the build ID, or catenates "+1" as the (new) build ID if there wasn't one yet.
 r←⍬
 'Invalid right argument; must be a Boolean)'⎕SIGNAL 11/⍨1≠+/(⊂bumpBuild)∊0 1
 :If bumpBuild
     (name version)←2↑##.Registry.Version
     date←10↑##.APLTreeUtils2.FormatDateTime ⎕TS
     :If 3=version+.='.'
         (version build)←{⍵{((¯1+⍵)↑⍺)(⍵↓⍺)}3⍳⍨+\'.'=⍵}version
         build←⍕1+⊃(//)⎕VFI build
     :ElseIf 2=version+.='.'
     :AndIf '+'∊version
         (version build)←{⍵{((⍵-1)↑⍺)(⍵↓⍺)}⍵⍳'+'}version
         build←⍕1+⊃(//)⎕VFI build
     :Else
         build←'1'
     :EndIf
     version,←'+',⍕build
     'Missing dots in version number'Assert 2=version+.='.'
     'Some bits in the version number are empty when they must not'Assert 0∧.<≢¨'.'(≠⊆⊢)version
     body←##.Registry.⎕NR'Version'
     newVersion←'r←''',name,''' ''',version,''' ''',date,''''
     body←'r←''[^$]+'⎕R newVersion⊣body
     Assert' '=1↑0⍴##.Registry.⎕FX body
     wsPath←(⍕⎕THIS.##),'.Registry.Version'
     Assert 0<≢⎕SE.Link.Add wsPath
 :EndIf
⍝Done
