 {r}←{force}CheckBuildList folder;ts1;ts2;filename1;filename2;deps;tree;b;newTree;deps2;ind;deps_
⍝ Checks the last modification date of both the build list and the dependency file.\\
⍝ We distinguish here between level-1 packages and other packages (=dependencies).
⍝ The level-1 packages are called top-level packages.
⍝ If the dependency file was modified after the build list was saved:
⍝ * If a top-level package was removed from the dependency file the build list is re-created
⍝ * If a dependency package is missing, re-installation is required and therefore an error thrown\\
 force←{0<⎕NC ⍵:⍎⍵ ⋄ 0}' force'
 folder←Reg.AddSlash folder
 filename2←folder,Reg.DependenciesFilename
 filename1←folder,Reg.BuildListFilename
 :If ~force
     ts1←1 ⎕DT⊃('type' 3)F.Dir filename1
     ts2←1 ⎕DT⊃('type' 3)F.Dir filename2
 :EndIf
 :If force
 :OrIf r←ts2>ts1
     :If F.IsFile filename1
         tree←Reg.GetJsonFromFile filename1
         tree←⍉↑tree.(principal packageID url)
         deps←(1⊃F.NGET filename2 1)~⊂''
         deps_←RemoveAlias¨deps
         b←deps_∊RemoveAlias¨tree[;2]                       ⍝ Check consistency
         :If ∨/~b
             ('Invalid entry: ',(⊃{⍺,', ',⍵}/(~b)/deps_),'; re-install!')Assert 0
         :EndIf
     :EndIf
     deps←⊃F.NGET(folder,Reg.DependenciesFilename)1
     0 CompileBuildList folder deps
 :EndIf
⍝Done
