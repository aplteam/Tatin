 r←{majorVersions}CheckForLaterVersion_ packageID;fullPath;url;fullPath2;packageID2;errMsg;statusCode;packageID_;buff
⍝ Takes a packageID and checks whether it finds a later version.
⍝ All Registries with a priority greater than 0 are scanned.\\
⍝ If `majorVersions` is 0 only minor and patch number are considered.\\
⍝ If `majorVersions` is 1 later "major" versions are considered as well.\\
⍝ For every package found these pieces of information are returned:
⍝ 1. Original package ID
⍝ 2. Package ID of the latest version found  (or "<<< No response >>>" or "<<< Not found >>>")
⍝ 3. URL
⍝ 4. Boolean that is 1 in case of success and 0 ("Not found" or "No response") otherwise
 majorVersions←{0<⎕NC ⍵:⍎⍵ ⋄ 0}'majorVersions'
 'Invalid left argument'Assert 1=≢majorVersions
 'Invalid left argument'Assert majorVersions∊0 1
 packageID_←RemoveAlias packageID
 (url buff)←('⎕'=⊃⊃⎕XSI)EstablishRegistry Reg.RemoveVersionNumber packageID_
 packageID_←buff,(≢Reg.RemoveVersionNumber packageID_)↓packageID_
 :If 0=≢url
     r←packageID_'<<< Not found anywhere >>>' '' 0
 :Else
     fullPath←url,packageID_
     PrintToSession'Checking <',fullPath,'>...'
     :Trap 345
         :If majorVersions
             (statusCode errMsg fullPath2)←GetBestVersionNumber(Reg.RemoveVersionNumber packageID_)url
         :Else
             (statusCode errMsg fullPath2)←GetBestVersionNumber(Reg.RemoveMinorAndPatch packageID_)url
         :EndIf
     :Else
         r←packageID_'<<< No response >>>'url 0
     :EndTrap
     :If 404=statusCode
         r←packageID_'<<< Not found >>>'url 0
     :Else
         errMsg Assert 200=statusCode
         :If 0<≢fullPath2
             packageID2←GetPackageIDFrom fullPath2
             r←packageID_ packageID2 url(packageID_≢packageID2)
         :EndIf
     :EndIf
 :EndIf
⍝Done
