 (opCode reg packageID)←EstablishRegistry_(uri packageID askUserFlag);stopFlag;list;options;msg;qdmx
⍝ Attempts to interrogate a Tatin Server. In case the server is down (or anything else goes awry)
⍝ then `askUserFlag` determines what to do:
⍝ * If it is 0 an error is thrown
⍝ * If it is 1 the user is asked what to do: try again, skip, or cancel
⍝ The decision is reflected by `opCode`, which is "process" or "skip"
 stopFlag←0
 opCode←'done'
 reg←⍬
 :Repeat
     :Trap 345 347
         :If 0=≢list←ListPackages_ uri,packageID
             stopFlag←1
             :Continue
         :Else
             reg←uri
             stopFlag←1
             opCode←'process'
             packageID←(Reg.RemoveVersionNumber⊃list[1;]),(≢Reg.RemoveVersionNumber packageID)↓packageID  ⍝ Get the casing right
         :EndIf
     :Else
         :If askUserFlag
             msg←(uri,' did not respond when interrogated for ',packageID)
             options←'Try again'('Skip ',uri)
             :Select msg Select options
             :Case 1
                 stopFlag←0
             :Case 2
                 stopFlag←1
                 opCode←'skip'
             :Else
                 (uri,' did not respond, cancelled by user',uri)⎕SIGNAL qdmx.EN
             :EndSelect
         :Else
             qdmx←⎕DMX
             :If 'ERROR 345'≡qdmx.EM
                 ('Did not respond: ',uri)⎕SIGNAL 347
             :Else
                 qdmx.EM ⎕SIGNAL qdmx.EN
             :EndIf
         :EndIf
     :EndTrap
 :Until stopFlag
⍝Done
