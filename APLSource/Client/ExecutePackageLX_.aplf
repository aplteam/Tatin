 {r}←ExecutePackageLX_(name cfg homePath);ref;valence;qdmx
⍝ Checks whether "lx" exists as a config parameter and is not empty.
⍝ If that's the case it is supposed to be the name of a function that will be executed.
 r←⍬
 :If 2=cfg.⎕NC'lx'
 :AndIf 0<≢cfg.lx
     ref←⍎name
     'Package LX is set but it is not a function'Assert 3=ref.⎕NC cfg.lx
     valence←1 2⊃ref.⎕AT cfg.lx
     :Trap 0
         :If 0=valence      ⍝ Niladic fn
             ref.TatinVars.LX←ref.{6::⍬ ⋄ ⍎⍵}cfg.lx
         :ElseIf 1=valence  ⍝ Monadic fn
             ref.TatinVars.LX←homePath ref.{6::⍬ ⋄ ⍎⍵}cfg.lx,' ''',homePath,''''
         :Else
             'lx is neither a niladic nor a monadic function'Assert 0
         :EndIf
     :Else
         qdmx←⎕DMX
         ⎕←'Executing the package initialising function (',cfg.lx,') for ',name,' has crashed with a ',qdmx.EM,'; RC=',⍕qdmx.EN
     :EndTrap
 :EndIf
⍝Done
