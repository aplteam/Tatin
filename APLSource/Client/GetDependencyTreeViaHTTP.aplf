 {r}←level GetDependencyTreeViaHTTP url;uri;packageID;request;res;port;isNotHttp;b;tree;requiredBy;alias;deps
⍝ Fetch the dependency tree via HTTP. Once a package is retrieved via the http protocol
⍝ we know that all dependencies must be fetched via the http protocol as well.\\
⍝ Requires semi-global ∆TREE
 r←⍬
 EstablishRumbaClients ⍬
 (uri packageID)←Reg.SplitAtLast url
 (alias uri)←SplitAtAlias uri
 request←R.NewRequest 0
 request.RequestTarget←Reg.VersionOf_REST,'/packages/dependencies/',packageID
 request.Headers,←⊂'Accept' 'application/json'
 res←SendAndReceive uri request
 :If 200≠res.StatusCode
     :If 0<≢res.Content
         ('Server: ',res.Content)Assert 0
     :Else
         ('Failed with status code ',⍕res.StatusCode)Assert 0
     :EndIf
 :EndIf
 :If 0<≢deps←Reg.JSON res.Content
 :AndIf 0<≢deps.data
     deps.data←((⊂⊂'null')≢¨2⊃¨deps.data)/deps.data
 :AndIf 0<≢deps.data
     ScanRegistries⊃,/2⊃¨deps.data
 :EndIf
⍝Done
