 r←{convert}GetPackageIDFrom packageID;buff
 ⍝ ⍵ is a filename or a URI, possibly fully qualified.
 ⍝ ← is the package ID
 ⍝ By default ⍺ is 0, meaning the package ID is returned unchanged.
 ⍝ If ⍺ is 1 the package ID is massaged so that it is an APL compatible name, meaning that all
 ⍝ characters that are not ASCII or are not allowed in an APL name are replaced by an underscore character.
 ⍝ Examples:
 ⍝ 'Example_Package_A-1.0.0'  ←   GetPackageIDFrom 'file://C:/Temp/Example-Package_A-1.0.0.zip'
 ⍝ 'aplteam-MarkAPL'  ←   GetPackageIDFrom 'https://tatin.dev/aplteam/MarkAPL'
 ⍝ 'MarkAPL'  ←   GetPackageIDFrom 'MarkAPL'
 ⍝ 'Example-Package_A_1_0_0'  ← 1 GetPackageIDFrom 'file://C:/Temp/Example-Package_A-1.0.0.zip'
 ⍝ 'aplteam-APLProcess-0.7.0' ← 1 GetPackageIDFrom 'https://tatin.dev/aplteam-APLProcess'
 convert←{0<⎕NC ⍵:⍎⍵ ⋄ 0}'convert'
 packageID←A.(DTB DTB)packageID
 :If '.zip'≡⎕C ¯4↑packageID
     r←{⍵↑⍨1+-⌊/(⌽⍵)⍳'/\]'}¯4↓packageID
 :ElseIf 'file://'{⍺≡(≢⍺)↑⍵}packageID
     buff←(≢'file://')↓packageID
     r←{⍵↑⍨-¯1+⌊/'/\'⍳⍨⌽⍵}buff
 :ElseIf IsHTTP packageID
     buff←RemoveHttpProtocol packageID
     buff←(~'//'⍷buff)/buff
     r←{'-'@(⍸'/'=⍵)⊢⍵}{⍵↓⍨⍵⍳'/'}buff
 :ElseIf ∨/'/\]'∊packageID
     r←{⍵↑⍨1+-⌊/(⌽⍵)⍳'/\]'}packageID
 :Else
     r←packageID
 :EndIf
 :If convert
     r←'_'@(⍸~r∊⎕D,⎕A,'abcdefghijklmnopqrstuvwxyz_',⎕UCS 10)⊣r
 :EndIf
⍝Done
