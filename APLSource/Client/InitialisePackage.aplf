 InitialisePackage←{
⍝ Takes a folder (⍵) and initialises this folder so that it can be a package.
⍝ This means in particular to create the folder (although it might already exist) and to
⍝ create a package config file with default settings or what ⍺ specifies: one
⍝ can pass a config namespace as ⍺, typically created by a call to InitPackageConfig,
⍝ and then amended.
⍝ There must be no config file yet, otherwise an error is thrown.
     ⍺←''
     cfg←⍺
     folder←⍵
     _←F.MkDir folder
     ('file "',CFG_Name,'" already exists')Assert 0=F.Exists folder,'/',CFG_Name:1
     cfg←1 WritePackageConfigFile folder cfg
     fn←folder,'/',cfg.source
     _←{F.MkDir ⍵}⍣(0=F.Exists fn)⊣fn
     1:cfg
 }
