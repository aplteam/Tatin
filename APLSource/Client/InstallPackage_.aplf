 r←targetFolder InstallPackage_(alias packageID);buildList;principal;packageID2;installFolder;msg;path2cache;url;bool;tempDir;cacheFlag;success;qdmx;cfg;packageID3;ind
⍝ Fetches a package and unzips it into a temp folder.
⍝ When caching is on, the temp folder is created within the top-level of the cache folder.
⍝ Otherwise a folder is created inside the temp directory of the given OS.
⍝ ← is a vector of names with the principle packages, including any alias(es).
 r←''
 :If 0<≢buildList←GetDependencyTree alias,packageID
     PrintToSession'  ',(⍕≢buildList),' dependenc',((1+1=≢buildList)⊃'ies' 'y'),' identified'
     CheckAliasOnInstall targetFolder alias(GetPackageIDFrom packageID)
     :For principal packageID2 url :In ↓buildList
         (alias packageID2)←SplitAtAlias packageID2
         installFolder←(Reg.AddSlash targetFolder),packageID2
         :If principal
             ('Installation folder "',installFolder,'" is a file')Assert 0=F.IsFile installFolder
         :EndIf
         :If F.IsDir installFolder
             F.RmDir installFolder
         :AndIf F.IsDir installFolder
             ⎕DL 0.01
             msg←'Could not remove install folder (package was apparently already installed):',CR
             msg,←installFolder
             msg Assert 0
         :EndIf
         :If MyUserSettings.caching
         :AndIf 0<≢path2cache←CompilePath2PackageInCache url packageID2
         :AndIf 0=GetNoCachingFlag url
             PrintToSession'  Fetching ',packageID2,' from Tatin package cache...'
             tempDir←GetPackageFromCache url packageID2 path2cache
             cacheFlag←0
         :Else
             :Trap ((⊂,##.Client.DEBUG)∊,¨'on' 1)↓Reg.ErrorNo
                 (success tempDir cacheFlag)←InstallPackage__ packageID2 url
                 :If 0=success
                     :Return
                 :EndIf
             :Else
                 qdmx←⎕DMX
                 qdmx.EM Assert 0
             :EndTrap
         :EndIf
         packageID3←{⍵↑⍨¯1+⍵⍳'+'}{⍵.(group,'-',name,'-',version)}Reg.APLfromJSON⊃⎕NGET(tempDir,'/apl-package.json')
         ind←(RemoveAlias¨buildList[;2])⍳(⊂packageID3)
         :If ind≤≢buildList
             buildList[ind;2]←⊂(RemoveAlias{(-≢⍺⍺ ⍵)↓⍵}2⊃buildList[ind;]),packageID3 ⍝ Corrects wrong casing according to the config file but preserve an alias, if any
             packageID2←packageID3
         :EndIf
         :If IsHTTP url
         :AndIf cacheFlag
             installFolder(⎕NCOPY Reg.F.ExecNfunction)tempDir
         :Else
             installFolder(⎕NMOVE Reg.F.ExecNfunction)tempDir
         :EndIf
         :If principal
             PrintToSession'    Add ',packageID2,' to dependency file...'
             targetFolder Dependencies.Add alias,packageID2
         :Else
             cfg←Reg.ReadPackageConfigFile installFolder
             :If 0<cfg.⎕NC'userCommandScript'
             :AndIf 0<≢cfg.userCommandScript
                 ⎕NDELETE installFolder,'/',⊃,/1↓⎕NPARTS cfg.userCommandScript  ⍝  ⍝TODO⍝ Remove once User Command Framework stops scanning cmddirs for more than 1 level
             :EndIf
         :EndIf
     :EndFor
     buildList[;3]←ReplaceCachePathByURL¨buildList[;3]
     PrintToSession'    Saving build list... '
     r←buildList[1;2]
     SaveBuildList buildList targetFolder
 :EndIf
