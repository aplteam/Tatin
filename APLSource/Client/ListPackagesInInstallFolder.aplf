 r←ListPackagesInInstallFolder path;json;buff;ns
⍝ Takes a path to a folder that must host a file "apl-buildlist.json" - that makes it an install folder.
⍝ Returns a matrix with zero or more rows and these columns:
⍝  * Full package name, potentially with an alias
⍝  * Boolean; 1 for principal packages
⍝  * Source the package came from
⍝  * Boolean that is 1 if the package is actually installed and 0 otherwise
 r←0 4⍴'' 1 '' 1
 path←Reg.AddSlash path
 :If 0<≢json←⊃F.NGET path,'/apl-buildlist.json'
     ns←Reg.APLfromJSON json
     r←⍉↑ns.(packageID principal url)
     r,←F.IsDir path∘,¨'/',¨RemoveAlias¨r[;1]
 :EndIf
