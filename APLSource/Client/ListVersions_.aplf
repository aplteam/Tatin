 list←{dateFlag}ListVersions_ url;request;res;host;packageID
 dateFlag←{0<⎕NC ⍵:⍎⍵ ⋄ 0}'dateFlag'
 (host packageID)←Reg.SeparateUriAndPackageID url
 packageID←{0=≢⍵:'*' ⋄ ⍵}packageID
 request←R.NewRequest 0
 request.RequestTarget←Reg.VersionOf_REST,'/packages/versions/',packageID
 :If dateFlag
     request.RequestTarget,←'?publishingdate=1'
 :EndIf
 request.Headers,←⊂'Accept' 'application/json'
 res←SendAndReceive host request
 :If 200=res.StatusCode
     list←⎕JSON⍠('Dialect' 'JSON5')⊣res.Content
     list←↑⍣dateFlag⊣list
 :ElseIf 404=res.StatusCode
     list←''
 :Else
     :If 0<≢res.Content
         ('Server: ',res.Content)Assert 0
     :Else
         ('Failed with status code ',⍕res.StatusCode)Assert 0
     :EndIf
 :EndIf
⍝Done
