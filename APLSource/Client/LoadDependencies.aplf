 {r}←{flags}LoadDependencies y;path;tree;dep2;saveIn;cfg;msg;packageID;saveIn2;ref;url;principalFlag;refs;alias;installFolder;targetSpace;packageID2;qdmx;isUC;makeHomeRelative;overwrite
⍝ Requires at least one argument but accepts up to two:
⍝ * Folder with a build list (mandatory)
⍝ * Name of the target namespace\\
⍝ If no target namespace was specified then by default packages are loaded into `#` except when the folder
⍝ is `[MyUCMDs]` in which case they are loaded into `⎕SE.
⍝ Via the optional left argument you may specify 0, 1 or 2 flags which default to 0:
⍝ * `overwrite` (1) enforces a load even if the package already exists in (#|⎕SE)._tatin
⍝ * `makeHomeRelative` (2) enforces the HOME path of the package to be relative: only the package folder
⍝   and the parent of that folder survice.
⍝ Specify a 3 as `⍺` in order to switch both flags on.\\
⍝ Loads all packages and injects required references into `targetSpace`.\\
⍝ Returns a vector with references to the loaded packages (no dependencies: principal packages only).
 :If 0=⎕NC'flags'
     overwrite←makeHomeRelative←0
 :Else
     (overwrite makeHomeRelative)←2↑Reg.IntToBits flags
 :EndIf
 'Length error on right argument'Assert(≢y)∊1 2
 (installFolder targetSpace)←2↑(⊆y),⊆''
 :If isUC←'[myucmds]'{⍺≡⎕C(≢⍺)↑⍵}installFolder
     installFolder←GetMyUCMDsFolder(≢'[myucmds]')↓installFolder
 :EndIf
 ('Folder does not exist:',⍕installFolder)Assert ⎕NEXISTS installFolder
 :If 0=≢targetSpace
     targetSpace←(1+isUC)⊃'#' '⎕SE'
 :EndIf
 r←refs←⍬
 targetSpace←⍕targetSpace
 :If 0=⎕NC targetSpace
     targetSpace Reg.CreateNamespace ⎕THIS
 :EndIf
 :If 0=≢F.ListFiles installFolder
     :Return
 :EndIf
 CheckBuildList installFolder
 :If 0<≢tree←LoadBuildList installFolder
     :For principalFlag packageID url :In ↓tree
         (alias packageID)←{~'@'∊⍵:''⍵ ⋄ '@'(≠⊆⊢)⍵}packageID
         path←installFolder,'/',packageID
         ('Cannot find package "',path,'"')Assert F.Exists path
         saveIn←GetTatinRootSpace 3⍴targetSpace
         cfg←ReadPackageConfigFile path
         packageID2←1 GetPackageIDFrom path
         saveIn,←'.',packageID2
         :If 0=⎕NC saveIn                       ⍝ If it already exists we don't do anything...
         :OrIf overwrite                        ⍝ ... unless we are are explicitly asked to
             :If overwrite
                 {(⍎⍺).⎕EX ⍵}/¯1 0↓¨'.'A.SplitPath saveIn
             :EndIf
             EstablishSystemVars saveIn cfg
             Reg.ImportWithQuadFIX(path,'/',cfg.source)(saveIn)
             ref←⍎saveIn
         :Else
             ref←⍎saveIn
         :EndIf
         makeHomeRelative EstablishStuffInTatinVars ref cfg path
         refs,←ref
         :If 1=principalFlag
             r,←ref
         :EndIf
         :If 1=principalFlag
             :Trap Reg.ErrorNo
                 LinkTargetToPackage cfg saveIn targetSpace alias
             :Else
                 qdmx←⎕DMX
                 ⎕DL 0.01
                 qdmx.EM ⎕SIGNAL qdmx.EN
             :EndTrap
         :EndIf
     :EndFor
     LinkDependencies refs
     ExecutePackageLX installFolder targetSpace tree
 :EndIf
⍝Done
