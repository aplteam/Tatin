 request ScanRegistries_2 registries;bool;i;registry;res;ns;bool2;qdmx;dm
⍝ Part of creating a build list. Establish the Server the package is hosted on.
 i←1
 :While 0<+/bool←0=≢¨∆TREE.Data[;∆TREE.∆url]
     registry←i⊃registries
     :If Reg.IsHTTP registry
         request.RequestTarget←Reg.VersionOf_REST,'/packages/exist/',1↓∊',',¨{'@'∊⍵:⍵↓⍨⍵⍳'@' ⋄ ⍵}¨bool/∆TREE.Data[;∆TREE.∆packageID]
         :Trap 345
             res← SendAndReceive registry request
         :Else
             qdmx←⎕DMX
             dm←' '~⍨1⊃qdmx.DM
             :If 'Handshakecertificateisfailing'≡dm
             :OrIf 'Couldnotconnecttohostdataport'≡dm
                 :If IsRunningAsUserCommand
                     PrintToSession'*** Could not reach ',registry,', therefore it is ignored'
                     :Continue
                 :Else
                     345 ⎕SIGNAL⍨'Could not reach ',registry  ⍝ When called via the API we stop
                 :EndIf
             :Else
                 qdmx.EM ⎕SIGNAL qdmx.EN
             :EndIf
         :EndTrap
         :If 200=res.StatusCode
             ns←Reg.JSON res.Content
             :If ∨/ns.exist
                 ∆TREE.Data[⍸bool\ns.exist;∆TREE.∆url]←⊂registry
             :EndIf
         :EndIf
     :Else
         bool2←F.IsDir registry∘,¨bool/∆TREE.Data[;∆TREE.∆packageID]
         ∆TREE.Data[⍸bool\bool2;∆TREE.∆url]←⊂registry
     :EndIf
 :Until (≢registries)<i←i+1
