 cfg←ValidateAPI2(cfg projectPath);extension;type;extensions;files
 extension←⎕C 3⌷⎕NPARTS cfg.source
 extensions←'.aplf' '.aplo' '.aplc' '.apli' '.apln' '.apla' '.dyalog'
 :If 0<≢cfg.api
     :If extension∊extensions
         type←(extensions⍳extension)⊃'Function' 'Operator' 'Class' 'Interface' 'Namespace' 'Array' 'Script'
          ('Source ',cfg.source,' (',type,') does not exist')Assert ⎕NEXISTS projectPath,cfg.source
     :Else
         files←2⊃∘⎕NPARTS¨{(≢⍵)↓¨⊃F.Dir ⍵,'/*'}projectPath,cfg.source
         'API either does not exist or is a function/operator'Assert(1↑'.'(≠⊆⊢)cfg.api)∊files
     :EndIf
 :EndIf
⍝Done
