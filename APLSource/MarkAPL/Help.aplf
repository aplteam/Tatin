 {ns}←{parms}Help recompileFlag;filename;path;filenames;ind
⍝ Displays any of the MarkAPL help files with your default browser.\\
⍝ `recompileFlag`:
⍝ * 0 shows the HTML file as it stands.
⍝ * 1 lets MarkAPL recompile it from MarkAPL_CheatSheet.md first.
⍝ Returns either `⍬` or the `ns` namespace created by `Init` and modified by `Process`.
⍝ However, if the markdown file does not exist then `recompileFlag` has no effect (is always 0)
⍝ because then there is nothing that could be recompiled.
 :Access Public Shared
 parms←{0<⎕NC ⍵:⍎⍵ ⋄ CreateHelpParms}'parms'
 :If 0=≢parms.homeFolder
     path←TatinVars.GetFullPath2AssetsFolder,'/'
 :Else
     path←parms.homeFolder,'/Files/'
 :EndIf
 filenames←FilesAndDirs.ListFiles path,'*.html'
 ind←'SelHelpDocForView@Select a document you want to put on view:'CommTools.Select{2⊃⎕NPARTS ⍵}¨filenames
 →(0=≢ind)/0
 filename←ind⊃filenames
 :If ~⎕NEXISTS(⊃,/2↑⎕NPARTS filename),'.md'
     recompileFlag←0
 :EndIf
 :If recompileFlag
     filename←(⊃,/2↑⎕NPARTS filename),'.md'
 :EndIf
 filename←recompileFlag AddAppropriateExtension filename
 ns←CompileHelp filename recompileFlag parms
 :If ¯1≠×recompileFlag  ⍝ This syntax is used only by `Make` and test cases, therefore it is not documented.
 :AndIf parms.viewInBrowser
     ns ShowHtml(⊃,/2↑⎕NPARTS filename),'.html'
 :EndIf
⍝Done
