:Class OS
⍝ This class offers methods that return the same result under Windows, Linux (but not Raspberry PI) and Mac OS.
⍝ Examples are `GetPID` and `KillPID`.\\
⍝ Exceptions are the functions `ShellExecute` (Linux/Mac only) and `WinExecute` (Windows only). They
⍝ perform very similar tasks but with very different parameters and results, so they were separated.\\
⍝ Kai Jaeger\\
⍝ Homepage: <https://github.com/aplteam/OS>


    ⎕IO←1 ⋄ ⎕ML←1

    ∇ r←Version
      :Access Public Shared
    ⍝ Returns the version number from `TatinVars` if possible.
    ⍝ See also `History`
      :If 0<⎕NC'TatinVars.CONFIG'
          r←(⎕JSON⍠('Dialect' 'JSON5')⊢TatinVars.CONFIG).version
      :ElseIf 0<##.⎕NC'TatinVars.CONFIG'
          r←(⎕JSON⍠('Dialect' 'JSON5')⊢##.TatinVars.CONFIG).version
      :Else
    ⍝ When a script was not loaded with Tatin, there will be no namespace `TatinVars`.
    ⍝ In such cases we cannot provide a version number, so we return just ''.
          r←''
      :EndIf
    ∇

    ∇ r←History
      :Access Public shared
      ⍝ * 4.0.0 ⋄ 2025-03-28
      ⍝   * `Version` has changed its syntax: it returns just a simple string with the version number now 
      ⍝     which comes from the package config file.
      ⍝   * `Make` improved.
      ⍝ * 3.2.0 ⋄ 2024-12-25
      ⍝   * The `Version` functions reports now what's defined in the package config file
      ⍝   * Packages updated
      ⍝ * 3.1.1 ⋄ 2023-05-08
      ⍝   * APLTreeUtils2 updated which was buggy (wrong version number)
      ⍝ * 3.1.0 ⋄ 2023-05-02
      ⍝   * New method `GetTaskList` added
      ⍝   * `History now returns a useful result
      ⍝ * 3.0.1 ⋄ 2021-03-01
      ⍝   * Package config file corrected (new format)
      ⍝   * APLTreeUtils2.Uppercase replaced by a call to 1 ⎕C
      ⍝ * 3.0.0 ⋄ 2020-09-07
      ⍝   * Does not :Include APLTreeUtils anymore (but requires the class APLTreeUtils2)
      ⍝   * Uses ⎕ML←1 and  ⎕IO←1 internally
      ⍝   * Needs at least Dyalog 18.0
      ⍝\\
      ⍝ For information regarding older versions see <https://github.com/aplteam/OS/releases>
      r←↑{⍵↓⍨+/∧\∧⌿(↑⍵)=' '}{⍵↓⍨⍵⍳'⍝'}¨{⍵/⍨'⍝'=⊃¨⍵}{⍵↓⍨+/∧\(↑⍵)=' '}¨1↓⎕NR⊃⎕SI
    ∇

    ∇ (rc more result)←ShellExecute cmd;buff
      :Access Shared Public
      ⍝ Simple way to fire up an application under Linux/Mac OS.\\
      ⍝ cmd must be a command line ready to be executed.
      ⍝ * `rc` is the exit code of the command executed.
      ⍝ * `more` is currently always an empty text vector.
      ⍝ * `result` is what's returned by the command executed.
      ⍝ You might consider adding </dev/null 1>/dev/null 2>/dev/null to a background call.
      result←more←''
      rc←0
      :Trap 11
          cmd←##.APLTreeUtils2.DTB cmd
          :If '&'=¯1↑cmd
              {}⎕SH(¯1↓cmd),' </dev/null >/dev/null 2>&1 &'
          :Else
              cmd,←' 2>&1; echo "CMDEXIT=$?"; exit 0'
              buff←⎕SH cmd
              rc←{2 6::⍬ ⋄ ⍎⍵}(≢'CMDEXIT=')↓⊃¯1↑buff
              :If 0≡rc
                  result←¯1↓buff
              :Else
                  more←¯1↓buff
              :EndIf
          :EndIf
      :Else
          rc←1
          more←⎕DMX.Message
      :EndTrap
    ∇

    ∇ r←{filter}GetTaskList y;addColumnTitles;bool;colTitles;doNoTouch;doNoTouchplatform;native;sep
    ⍝ Returns a table with all processes with Name and Process ID when `native` is 0.
    ⍝ If `native` is 1 the result depends on the current OS. The default is 0.
    ⍝ `addColumnTitles←1` (the default) adds two rows with columsn titles and separators.
    ⍝ `doNoTouch←1` does not cut the path on Mac-OS. Defaults to 0. Is ignored on other platforms
    ⍝ The optional left argument `filter` is, when not empty, used to catch a matching process.
    ⍝ In case `filter` is not numeric the names are scanned (case independent), otherwise the process IDs.
      :Access Public Shared
      (native addColumnTitles doNoTouch)←3↑y,(≢y)↓0 1 0
      filter←{0<⎕NC ⍵:⍎⍵ ⋄ ''}'filter'
      '"addColumnTitles" must be a Boolean'Assert addColumnTitles∊0 1
      '"native" must be a Boolean'Assert native∊0 1
      platform←⎕C 3↑⊃#.⎕WG'APLVersion'
      :Select platform
      :Case 'win'
          r←⎕CMD'tasklist'
          r/⍨←0<≢¨r
          sep←2⊃r
          bool←sep≠' '
          colTitles←bool∘⊆¨2↑r
          r←2↓r
          r←↑bool∘⊆¨r
          r←1 SortTable r
          :If native
              :If addColumnTitles
                  r←(↑colTitles),[1]r
              :EndIf
          :Else
              r←r[;1 2]
              r[;2]←{⊃⊃(//)⎕VFI ⍵}¨r[;2]
              :If 0<≢filter
                  :If ' '=1↑0⍴filter
                      r←(∨/(⎕C filter)⍷⎕C↑r[;1])⌿r
                  :Else
                      r←(filter=r[;2])⌿r
                  :EndIf
              :EndIf
              :If addColumnTitles
                  r←(↑2↑¨colTitles),[1]r
              :EndIf
          :EndIf
      :Case 'lin'
          r←⎕SH'ps -A'
          r←↑{' '(≠⊆⊢)⍵}¨{⍵/⍨~'  '⍷⍵}¨r
          (1↓r[;1])←{⊃⊃(//)⎕VFI ⍵}¨1↓r[;1]
          :If 0<≢filter
              :If ' '=1↑0⍴filter
                  r←(1,∨/(⎕C filter)⍷⎕C↑1↓r[;4])⌿r
              :Else
                  r←(1,filter=1↓r[;1])⌿r
              :EndIf
          :EndIf
          (1↓r)←1 SortTable 1↓r
          :If ~addColumnTitles
              r←1↓r
          :EndIf
          :If ~native
              r←r[;4 1]
          :EndIf
          :If addColumnTitles
              r←(1 0,(¯1+≢r)⍴1)⍀r
              r[2;]←(≢¨r[1;])⍴¨'='
          :EndIf
      :Case 'mac'
          r←⎕SH'ps -A'
          r←{' '(≠⊆⊢)⍵}¨{⍵/⍨~'  '⍷⍵}¨r
          r←↑4↑¨r
          (1↓r[;1])←{⊃⊃(//)⎕VFI ⍵}¨1↓r[;1]
          :If 0=doNoTouch
              r[;4]←{⍵{⍺↑⍨-¯1+⍵⍳'/'}⌽⍵}¨r[;4]
          :EndIf
          :If 0<≢filter
              :If ' '=1↑0⍴filter
                  r←(1,∨/(⎕C filter)⍷⎕C↑1↓r[;4])⌿r
              :Else
                  r←(filter∊r[;1])⌿r
              :EndIf
          :EndIf
          (1↓r)←4 SortTable 1↓r
          :If ~addColumnTitles
              r←1↓r
          :EndIf
          :If ~native
              r←r[;4 1]
          :EndIf
          :If addColumnTitles
              r←(1 0,(¯1+≢r)⍴1)⍀r
              r[2;]←(≢¨r[1;])⍴¨'='
          :EndIf
      :Else
          'Unsupported OS?!'Assert 0
      :EndSelect
    ∇

    ∇ t←col SortTable t
    ⍝ Sort table `t` according to column `col`.
      :If 1<≢t
          t←t[⍋⎕C t[;col];]
      :EndIf
    ∇

    ∇ {(success rc more)}←{adminFlag}WinExecute x;ShellOpen;parms;flag
      :Access Public Shared
      ⍝ Simple way to fire up an application or a document.\\
      ⍝ Note that you **cannot** catch the standard output of any application executed with `WinExecute`.
      ⍝ However, you might be able to execute it with `WinExecBatch` which _can_ return the standard
      ⍝ output returned by whatever you've executed - see there.\\
      ⍝ `⍵` can be one of:
      ⍝ * A namespace, typically created by calling [`CreateParms_WinExecute`](#). This is called a
      ⍝   parameter space.
      ⍝ * A text string typically specifying a document or an EXE, possibly with command line parameters.\\
      ⍝ In case a text string is passed and the name of the file (first parameter: the EXE/document) contains
      ⍝ a space then this filename **must** be enclosed within double quotes.\\
      ⍝ Any other filename with spaces in the name must be enclosed by double-quotes as well.\\
      ⍝ A parameter space is usually created by calling `CreateParms_WinExecute`. You can then make
      ⍝ amendments to it and pass it as right argument. See there for details.\\
      ⍝ If the defaults are fine for you and you want just start an EXE or, say, display an
      ⍝ HTML file then you can just specify a path pointing either to the EXE or to the document.\\
      ⍝ You can even specify command line parameters this way but you **must** then enclose `file` with
      ⍝ double quotes (") even if the file does not contain any blanks. (The `ShellExecute` Windows function
      ⍝ does not like double quotes but they will be removed before it is called).\\
      ⍝ The optional left argument defaults to 0 which makes the verb default to "OPEN". By specifying
      ⍝ a 1 here it's going to be "RUNAS" meaning that the application is executed in elevated mode
      ⍝ (=with admin rights). Of course for this the user must have admin rights.\\
      ⍝ See the test cases for examples.\\
      ⍝ The function returns a three-element vector:
      ⍝ 1. A Boolean flag, 1 indicating success.
      ⍝ 2. The return code of the Windows API function `ShellOpen`. Is 42 in case of success.
      ⍝ 3. An empty text string in case of success. In case of failure this may provide additional information.
      'Runs under Windows only'Assert'Win'≡##.APLTreeUtils2.GetOperatingSystem ⍬
      success←0 ⋄ more←'' ⋄ rc←0
      :If (⎕DR x)∊80 82
          :If 0≠2|'"'+.=x
              more←'Odd nunmber of doubles quotes detected'
              :Return
          :EndIf
          parms←CreateParms_WinExecute
          :If '"'=1⍴x
              parms.(file lpParms)←x{(⍵↑⍺)(⍵↓⍺)}1++/∧\2>+\'"'=x
          :Else
              parms.(file lpParms)←x{(⍵↑⍺)(⍵↓⍺)}⌊/x⍳' "'
          :EndIf
      :ElseIf 326=⎕DR x
      :AndIf 9=⎕NC'x'
          parms←x
          parms.verb←1 ⎕C parms.verb
          :If 0≠2|'"'+.=parms.lpParms
              more←'Odd nunmber of doubles quotes in "lpParms" detected'
              :Return
          :EndIf
      :Else
          'Invalid right argument'Assert 0
      :EndIf
      'Invalid verb'Assert(⊂parms.verb)∊'EXPLORE' 'FIND' 'OPEN' 'PRINT' 'RUNAS' ''
      'ShellOpen'⎕NA'U Shell32.C32|ShellExecute* I <0T <0T <0T <0T I'
      adminFlag←{0<⎕NC ⍵:⍎⍵ ⋄ 0}'adminFlag'
      :If adminFlag
          parms.verb←'RUNAS'
      :EndIf
      parms.file~←'"'
      :Trap 0
          rc←ShellOpen parms.(handle verb file lpParms lpDirectory show)
          success←42=rc
      :Else
          rc←⎕EN
          more←⎕DMX.Message
          :Return
      :EndTrap
    ∇

    ∇ parms←CreateParms_WinExecute
      :Access Public Shared
      ⍝ This method returns a parameter space populated with default values that can be fed to the [`WinExecute`](#) method.
      ⍝ | **Parameter**| **Notes** |
      ⍝ | `verb`       | Must be one of: EDIT, EXPLORE, FIND, OPEN, PRINT, RUNAS, NULL (default). Note that "RUNAS" is "Open" with admin rights. |
      ⍝ | `file`       | Name of the file `verb` is performed on. Usually this is an EXE but it can be a document as well. |
      ⍝ | `handle`     | Handle pointing to a window or 0 (default). |
      ⍝ | `show`       | 1 (default) allows the application involved to show its windows. 0 hides any windows. |
      ⍝ | `lpParms`    | Command line parameters in case the verb is "OPEN". |
      ⍝ | `lpDirectory`| The working directory for the application involved. |
      ⍝
      ⍝ For more information see <https://msdn.microsoft.com/en-us/library/windows/desktop/bb762153(v=vs.85).aspx>
      parms←⎕NS''
      parms.verb←''
      parms.file←''
      parms.handle←0
      parms.show←1          ⍝ Allow the app to show its windows. Suppress with  0.
      parms.lpParms←''
      parms.lpDirectory←''
    ∇

    ∇ (success rc result)←{adminFlag}WinExecBatch cmd;batFilename;tempFilename;en;more;parms;i;flag
    ⍝ This method executes a command and returns its standard output on `result`.\\
    ⍝ **Don't** use this for programs that interact with a user! For example, don't use this to
    ⍝ fire up an interactive APL session; this cannot work because standard output is redirected.
    ⍝ (Use `WinExecute` for this which cannot capture standard output itself)\\
    ⍝ Performes the following actions:
    ⍝ * Puts `cmd` into a batch file which is a temp file
    ⍝ * Executes the batch file with `WinExecute`
    ⍝ * Circumvent the standard output of the batch file into another temp file
    ⍝ * Waits until the temp file makes an appearance
    ⍝ * Reads that temp file and returns the contents as `result`.\\
    ⍝
    ⍝ * `success` is a Boolean with 1 indicating success
    ⍝ * `rc` is a return code. 42 stands for "okay"
      :Access Public Shared
      'Runs under Windows only'Assert'Win'≡##.APLTreeUtils2.GetOperatingSystem ⍬
      tempFilename←GetTempFilename
      batFilename←(¯3↓tempFilename),'BAT'
      cmd,←' >',tempFilename
      ⎕NDELETE tempFilename
      (⊂cmd)⎕NPUT batFilename
      adminFlag←{0<⎕NC ⍵:⍎⍵ ⋄ 0}'adminFlag'
      parms←CreateParms_WinExecute
      parms.file←batFilename
      parms.show←0
      (success rc more)←adminFlag WinExecute parms
      :If success
          i←flag←0
          :Repeat
              i+←1
              ⎕DL 0.1
              :If IsFile tempFilename
                  result←⊃⎕NGET tempFilename
                  flag←(100<i)∨0<≢result
              :EndIf
          :Until flag
      :Else
          result←more
      :EndIf
      ⎕DL 0.1
      {}{19 22::⍬ ⋄ ⎕NDELETE ⍵}¨batFilename tempFilename
    ∇

    ∇ r←GetSharedLib
      :Access Public Shared
      :Select ##.APLTreeUtils2.GetOperatingSystem ⍬
      :Case 'Lin'
          r←GetLibcName ⍬
      :Case 'Mac'
          r←'/usr/lib/libc.dylib'
      :Else
          . ⍝ Huuh?!
      :EndSelect
    ∇

    ∇ r←GetPID;∆GetPID;∆GetCurrentProcessId
    ⍝ Returns the process ID of the current process ID.
    ⍝ In case of an error a 0 is returned.\\
    ⍝ See also [`KillPID`](#).
      :Access Public Shared
      :Select ##.APLTreeUtils2.GetOperatingSystem ⍬
      :Case 'Win'
          :Trap 11
              '∆GetCurrentProcessId'⎕NA'I KERNEL32|GetCurrentProcessId'
              r←∆GetCurrentProcessId
          :Else
              r←0
          :EndTrap
      :CaseList 'Mac' 'Lin'
          '∆GetPID'⎕NA'I4 ',GetSharedLib,'| getpid'
          :Trap 11
              r←∆GetPID
          :Else
              r←0
          :EndTrap
      :Else
          .⍝ Huuh?!
      :EndSelect
    ∇

    ∇ r←KillPID pid;False;OpenProcess;PROCESS_TERMINATE;TerminateProcess;h;res;thisPID;∆CloseHandle;∆OpenProcess;∆TerminateProcess
    ⍝ Kill one or more processes identified by their process ID.\\
    ⍝ See also [`GetPID`](#).
      :Access Public Shared
      r←0
      :Select ##.APLTreeUtils2.GetOperatingSystem ⍬
      :Case 'Win'
          '∆OpenProcess'⎕NA'U4 KERNEL32.C32|OpenProcess I4 I2 I4'
          PROCESS_TERMINATE←⊃83 323 ⎕DR 1
          False←⊃83 323 ⎕DR 0
          '∆CloseHandle'⎕NA'U KERNEL32.C32|CloseHandle I4'
          :Repeat
              thisPID←⊃pid
              'Invalid PID: not an integer'Assert 0=1↑0⍴thisPID
              :If 0≠h←∆OpenProcess PROCESS_TERMINATE False thisPID   ⍝ Get handle to the process
                  '∆TerminateProcess'⎕NA'KERNEL32.C32|TerminateProcess P I4'
                  {}∆TerminateProcess h 0                         ⍝ Kill it
                  r←1
              :End
              {}∆CloseHandle h
          :Until 0=≢pid←1↓pid
      :CaseList 'Lin' 'Mac'
          :Repeat
              r←⍬
              :Trap 0
                  res←⎕SH'kill -9 ',⍕pid
                  r,←1
              :Else
                  r,←0
              :EndTrap
          :Until 0=≢pid←1↓pid
          r←∧/r
      :Else
          . ⍝ Huuh?!
      :EndSelect
    ∇

⍝ Private stuff

      GetLibcName←{   ⍝ Linux: extract real name of libc that is actually used
          pid←⊃⎕SH'echo $PPID'
          libs←⎕SH'ldd /proc/',pid,'/exe'
          ⊃('^[[:space:]]*libc\.so\b.*=>[[:space:]]*([^[:space:]]*)'⎕S'\1')libs
      }

    GetAPL_Width←{z←⍵ ⋄ 2×⍬⍴⎕SIZE'z'}

    ∇ r←GetDyaLib
      r←'dyalog',(⍕GetAPL_Width ⍬),'.dylib'
    ∇

    ∇ filename←GetTempFilename;rc;start;no;fno;path
    ⍝ Copied from FilesAndDirs in 2019-10 in an attempt to make OS independent.
      path←GetTempPath
      start←no←⍎{(,'ZI2,ZI2,ZI2'⎕FMT 3↑⍵),⍕3↓⍵}3↓⎕TS  ⍝ Expensive but successful very soon
          ⍝ no←100⊥3↓⎕TS ⍝ Not reliable: can take a large number of tries before successful
      :Repeat
          filename←path,(⎕AN,'_',⍕no),'.tmp'
          :Trap 22
              fno←filename ⎕NCREATE 0
              ⎕NUNTIE fno
          :Else
              fno←0
          :EndTrap
          no+←10
      :Until (fno≠0)∨no>start+1000×10  ⍝ max 1000 tries
      'Unable to create temporay file'Assert fno≠0
    ∇

    ∇ path←GetTempPath;∆GetTempPath
    ⍝ Copied from FilesAndDirs in 2019-10 in an attempt to make OS independent.
    ⍝ Replace this by the `739 ⌶ 0` function once 17.0 becomes the latest supported version.
      :Select ##.APLTreeUtils2.GetOperatingSystem ⍬
      :Case 'Win'
          '∆GetTempPath'⎕NA'I4 KERNEL32.C32|GetTempPath* I4 >T[]'
          path←⊃↑/∆GetTempPath 1024 1024
          :If 0=≢path
              0 Assert⍨'Problem getting Windows temp path!; rc=',⍕GetLastError
          :EndIf
      :Case 'Lin'
          path←'/tmp/'
      :Case 'Mac'
          path←'/private/tmp/'
      :Else
          .⍝ Huuh?!
      :EndSelect
    ∇

    ∇ r←GetLastError;∆GetLastError
    ⍝ Copied from FilesAndDirs in 2019-10 in an attempt to make OS independent.
      :Select ##.APLTreeUtils2.GetOperatingSystem ⍬
      :Case 'Win'
          '∆GetLastError'⎕NA'I4 kernel32.C32|GetLastError'
          r←∆GetLastError
      :CaseList 'Lin' 'Mac'
          r←⎕SH'$errno'
          :If 0=≢r
              r←0
          :Else
              r←⊂(//)⎕VFI∊r
          :EndIf
      :Else
          .  ⍝ Huuh?!
      :EndSelect
    ∇

      IsFile←{
          0=⎕NEXISTS ⍵:0
          2=1 ⎕NINFO ⍵
      }

    Assert←{⍺←'' ⋄ (,1)≡,⍵:r←1 ⋄ ⎕ML←1 ⋄ ⍺ ⎕SIGNAL 1↓(⊃∊⍵),11}

:EndClass
