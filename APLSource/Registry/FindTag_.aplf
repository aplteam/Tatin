 (fuzzy bool)←{minPercentage}FindTag_(tagList search);tagList_;hits;search_;b;percentage
⍝ Tries to find `search` (one to many tags) in `tagList`.
⍝ Uses a fuzzy approach in case nothing fits with ≡ and ⍷.
⍝ Returns:
⍝ * (¯1 '') in case nothing fits
⍝ * (0 <list>) for an exact match
⍝ * (1 <list>) for a match with ⍷
⍝ * (2 <list>) for a fuzzy match
 minPercentage←{0<⎕NC ⍵:⍎⍵ ⋄ 50}'minPercentage'
 fuzzy←¯1
 bool←0
 :If 0<+/bool←search∘≡¨tagList
     fuzzy←0
 :ElseIf 0<+/bool←∨/search⍷↑tagList
     fuzzy←1
 :Else
     tagList_←{↓{⍵}⌺2⊣⍵}¨tagList
     search_←{↓⍵}⌺2⊣search
     b←tagList_∊¨⊂search_
     hits←+/¨(b/¨tagList_){2</⍺⍳⍵}¨⊂search_
     percentage←⌊0.5+100×hits÷≢¨tagList
     :If 0<+/bool←percentage≥minPercentage
         fuzzy←2
     :EndIf
 :EndIf
⍝Done
