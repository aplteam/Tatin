 {(statusCode errMsg)}←{isHttpRequest}PublishPackage y;packageID;zipFilename;registryPath;G;HttpCodes;tempDir;cfg;packageDir;list;zipFilename2
⍝ Tries to publish a package locally.\\
⍝ * If the package already exists `errmsg` carries an appropriate message. No further action is taken.
⍝ * If any action along the line fails (like creating the package directory, copying the ZIP file, copying the dependency file, if any)
⍝   then all actions carried out so far are rolled back.
⍝ Two right arguments are mandatory:
⍝ [1] Name of a zip file
⍝ [2] Path to the folder where the Registry data lives
⍝ The third argument is optional and only provided by a running server: it's the namespace G (Globals).
⍝ No matter whether this functions was called by a Tatin server or not, it returns HTTP status codes:
⍝ * In case of success 200
⍝ * In case the package is already published 400 (Bad request)
⍝ * In case something else went wrong 500 (Internal Server error)
⍝ In case the function is called by a Tatin server, `errMsg` should be returned as content; that's why we do not just use `Assert`.
 (zipFilename registryPath G)←3↑y,⊂⍬
 isHttpRequest←{0=⎕NC ⍵:0 ⋄ ⍎⍵}'isHttpRequest'
 HttpCodes←⎕NS'' ⋄ HttpCodes.OK←200 ⋄ HttpCodes.InternalServerError←500 ⋄ HttpCodes.BadRequest←400
 tempDir←F.GetTempSubDir''
 zipFilename ZipArchive.UnzipTo tempDir
 :If F.IsFile tempDir,'/',CFG_Name
     cfg←ReadPackageConfigFile tempDir
 :Else
     errMsg←'Package does not contain a file "',CFG_Name,'"'
     statusCode←HttpCodes.BadRequest
     →∆GetOut
 :EndIf
 :If 0<≢errMsg←CheckConfigProperties cfg G
     statusCode←HttpCodes.BadRequest
 :Else
     cfg←MassageTags cfg
     statusCode←HttpCodes.OK ⋄ errMsg←''
     :If 0=≢cfg.minimumAplVersion
         cfg.minimumAplVersion←'18.2' ⍝ This is the minimum by definition: Tatin does not run on earlier versions
     :EndIf
     :If 0=≢cfg.maintainer
         cfg.maintainer←'email'GetGroupData registryPath cfg.group
     :EndIf
     (JSON cfg)F.NPUT(tempDir,'/',CFG_Name)1
     packageID←{⍵.group,'-',⍵.name,'-',⍵.version}cfg
     packageID←⊃SplitVersionFromBuild packageID
     registryPath←AddSlash registryPath
     packageDir←registryPath,packageID
     list←GetAllPackages registryPath
     list←RemoveVersionNumber¨list
     :If ~(⊂RemoveVersionNumber 2⊃⎕NPARTS packageDir)∊list
     :AndIf (⊂⎕C RemoveVersionNumber 2⊃⎕NPARTS packageDir)∊⎕C list
         errMsg←'There is already such a package but with a different spelling (case)'
         statusCode←HttpCodes.BadRequest
     :ElseIf F.IsDir packageDir
         errMsg←'Package already exists (version conflict)'
         statusCode←HttpCodes.BadRequest
     :ElseIf 0=F.MkDir packageDir,'/data'
         statusCode←HttpCodes.InternalServerError
         errMsg←'Could not create data/ directory for ',packageID
     :ElseIf 0<≢(zipFilename2←packageDir,'/',packageID,'.zip')(⎕NCOPY Exec)zipFilename
         statusCode←HttpCodes.InternalServerError
         errMsg←'Publishing ',packageID,' failed due to a server error'
         packageDir RollbackPublishPackage errMsg
     :Else
         (packageDir,'/data')(⎕NCOPY⍠('Wildcard' 1)F.ExecNfunction)tempDir,'/*'
         :If 0<≢packageDir(⎕NMOVE Exec)packageDir,'/data/apl-package.json'
             statusCode←HttpCodes.InternalServerError
             errMsg←'Publishing ',packageID,' failed due to a server error'
             packageDir RollbackPublishPackage errMsg
         :Else
             (statusCode errMsg)←PublishPackage_ packageID packageDir tempDir zipFilename2 isHttpRequest
         :EndIf
     :EndIf
 :EndIf
∆GetOut:
 F.RmDir tempDir
⍝Done
