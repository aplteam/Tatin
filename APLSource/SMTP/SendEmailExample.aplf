 r←{filenames}SendEmailExample emailAddress;connectionParms;myConnection;mailParms;rc;msg;log
⍝ Use this to send a test email to `emailAddress`.
⍝ Use with care: the email address used for logging on to the SMTP server is not suitable for
⍝ anything remotely confidental; it's just for tests. Be aware that the hard-coded credentials
⍝ might get changed in case somebody uabuses this.
⍝ Note that the number of emails you may send is limited.
⍝ ⍺ is optional and may be a char vector (filename) or a vector of char vectors (filesnames).
⍝ If specified those are attached to the email. Relative filenames are expected in ./

 filenames←{0<⎕NC ⍵:,⊆⍎⍵ ⋄ ⍬}'filenames'

 connectionParms←⎕NEW ConnectionParameters
 connectionParms.Server←'smtp.gmail.com'
 connectionParms.Port←465
 connectionParms.Userid←'smtp.tatin.tests1@gmail.com'
 connectionParms.Password←'xjywwfkwjgeeacod'
 myConnection←⎕NEW Connection(,⊂connectionParms)

 mailParms←⎕NEW MailParameters
 mailParms.From←'do-not-reply@aplteam.com'
 mailParms.To←connectionParms.Userid
 mailParms.CC←emailAddress
 mailParms.Subject←'From APLTeam SMTP'
 mailParms.Body←'Test E-mail' ⍝ Can also be a vector of text vectors

 :If filenames≢⍬
     mailParms.AddAttachment¨filenames
 :EndIf

 (rc msg log)←myConnection.Send mailParms
 :If 0=rc
     r←'Email was sent successfully'
 :Else
     r←'Failed with rc=',(⍕rc),'; ',¯1↓(⊆msg),¨⎕UCS 13
 :EndIf
