 okayFlag←credentials AcceptCredential(apiKey groupName);groupNames;credentials;ind;apiKeys
⍝ Takes an API key and a group name and checks whether that is found in the array `credentials`.
⍝ Note that `⍺` is usually provided as `G.Credentials` (except test cases etc.).\\
⍝ This function is called when the user attempts to perform a PUT (read: "Publish") or
⍝ a DELETE (if ever) or any other operation that requires authentication.
 okayFlag←0
 :If 0<≢credentials                                         ⍝ Do we have any credentials saved at all?
     (groupNames apiKeys)←credentials
     :If 0=≢apiKeys~' '
         okayFlag←1
     :Else
         apiKey←A.DTB A.DLB apiKey
         :If 0=≢apiKey                                      ⍝ Did the user provide credentials
             :If (⊂groupName)∊groupNames                    ⍝ Does this group NOT require an API key?
                 okayFlag←1
             :Else
                 okayFlag←(,'*')≡(∊credentials)~⎕TS,' '     ⍝ Everybody anything?
             :EndIf
         :Else
             ind←apiKeys⍳⊂apiKey
             :If ind≤≢apiKeys                               ⍝ Must be known, otherwise we are done
                 :If (⊂groupName)∊groupNames                ⍝ Was that group assigned a specific API kay?
                     okayFlag←groupName≡ind⊃groupNames      ⍝ Then it must be that specific group
                 :Else
                     okayFlag←(,'*')≡,ind⊃groupNames        ⍝ Is it a "master kay" (all groups)?
                 :EndIf
             :EndIf
         :EndIf
     :EndIf
 :EndIf
⍝Done
