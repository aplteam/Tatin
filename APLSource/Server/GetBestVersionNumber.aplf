 response←request GetBestVersionNumber packageID;bestVersion;ns;group;noBetas
⍝ `path` is fully qualified. The packageID part might have just a major version no. or no version numer at all
 :If Reg.IsValidPackageName packageID
     :If 0<≢group←EstablishGroup packageID
         :If 1=≢group
             packageID←(1⊃group),'-',packageID
         :Else
             response←request Respond 400
             response.Content←'The package exists in multiple groups: ',⊃{⍺,',',⍵}/group
             :Return
         :EndIf
     :Else
         response←'Package not found'Return404 request
         :If 200≠response.StatusCode
             :Return
         :EndIf
     :EndIf
 :EndIf
 :If 0=≢noBetas←request R.GetHeaderValue'include-betas'
     noBetas←Reg.GetNoBetasDefault
 :Else
     noBetas←~noBetas∊'yY'
 :EndIf
 bestVersion←(≢G.RegistryPath)↓noBetas Reg.GetBestVersionNumber G.RegistryPath,packageID
 :If 0<≢bestVersion
     ns←⎕NS''
     ns.BestVersion←bestVersion
     response←request ReturnJSON Reg.JSON ns
 :Else
     response←'Package not found'Return404 request
 :EndIf
⍝Done
