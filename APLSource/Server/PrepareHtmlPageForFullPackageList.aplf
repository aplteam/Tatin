 html←packageIDs PrepareHtmlPageForFullPackageList data;col1;col2;col3;buff;col4;col5;col6;⎕TRAP;bool
 buff←{(≢⍵)}⌸(⍪Reg.RemoveVersionNumber¨data[;1]),1
 html←HtmlHeader GetDataTableStuff ⍬
 html,←⊂'<h2>List of all packages</h2>'
 html,←AddSpecialMessage ⍬
 html,←⊂'<p>List with ',(⍕≢data),' items from ',(⍕≢buff),' different packages</p>'
 html,←⊂H.OpenTable CalcTableWidth data
 html,←⊂'<thead>'
 html,←⊂'<tr class="my-first-th">'
 html,←'<th></th>' '<th>Pkg</th>' '<th>Name</th>' '<th>Published at</th>' '<th>Config</th>' '<th>Dependencies</th>'
 html,←⊂'</thead>'
 html,←⊂'<tbody>'
 :If 0<≢data
     col1←{'<td>',⍵,'</td>'}¨' →'[{1+(⍵⍳⍵)=⍳≢⍵}Reg.RemoveMinorAndPatch¨data[;1]]
     col2←{'<td>',⍵,'.</td>'}∘⍕¨⊃,/⍳¨buff
     col3←{'<td><code>',⍵,'</code></td>'}¨data[;1]
     col4←{'<td><code>',⍵,'</code></td>'}¨data[;2]
     col5←{'<td><a href="',Reg.VersionOf_REST,'/packages/details/',⍵,'" title="Link to the package configuration">Show</a></td>'}¨data[;1]
     bool←PackageHasDependencies¨data[;1]
     col6←bool{⍺:'<td><a href="',Reg.VersionOf_REST,'/packages/dependencies/',⍵,'" title="Link to the dependency definition">Show</a></td>' ⋄ '<td>None</td>'}¨data[;1]
     html,←⊃,/{(⊂'<tr>'),⍵,⊂('</tr>')}¨↓col1,col2,col3,col4,col5,⍪col6
 :EndIf
 html,←⊂'</tbody>'
 html,←⊂'</table>'
 html,←⊆GetFooter
 html,←⊂'</body>'
 html,←⊂'</html>'
 ⍝Done
