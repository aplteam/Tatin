 success←TatinAmendments G;congaPath
⍝ Called by Plodder's `Run` operator.
⍝ In charge for making the required amendments for running a Tatin server.
 ∆STARTED←60 ⎕DT⊂⎕TS
 ∆WS_HASH←GetWorkspaceHash
 CreateAllRefs
 P←##.Plodder
 P.G.RegistryPath←F.ExpandPath⊃P.G.INI.Get'CONFIG:Registry'
 P.LogFolder←P.G.INI.Get'LOGGING:Folder'
 P.G←ProcessCredentials P.G
 :If 0=P.G.INI.Exist'CONFIG:GroupHomePages'
     0 P.G.INI.Put'CONFIG:GroupHomePages'  ⍝ INI entry was created in 0.72.0, and the default is 0
 :EndIf

 :If 9=#.⎕NC'ErrorParms'
     #.ErrorParms.G←G
     #.ErrorParms.customFnsParent←'#.Tatin.Server'
     #.ErrorParms.customFns←'SendCrashMail'
 :EndIf
 ExecuteMaintenanceCode G
 congaPath←2 ⎕NQ #'GetEnvironment' 'DYALOG'
 :If 'Win'≢3↑⎕C 1⊃'.'⎕WG'APLVersion'
     congaPath,←'/lib'
 :EndIf
 ##.Admin.InitConga congaPath
 success←1
⍝Done
