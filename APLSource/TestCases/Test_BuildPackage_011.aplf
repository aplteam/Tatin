 r←Test_BuildPackage_011(stopFlag batchFlag);⎕TRAP;src2;src3;rc;msg;parms
 ;dir;src;dist;body;cfg;zipfile;ns;list;expected
⍝ Packaage that has a folder and a file as "assets" property.
 ⎕TRAP←(999 'C' '. ⍝ Deliberate error')(0 'N')
 r←T._Failed

 #.⎕EX TC.GetTatinRootSpace'#'
 dir←∆GetTempDir ⍬
 (src dist)←(⊂dir,'/'),¨'src' 'dist'
 src,←'/v0.1.0'
 F.MkDir src dist
 F.MkDir src2←src,'/TheSource'
 F.MkDir src3←src,'/Assets/DLLs'

 body←'r←Hello' 'r←''World'''
 (⊂body)F.NPUT src2,'/Hello.aplf'
 body←'r←Hello2' 'r←''World2'''
 (⊂body)F.NPUT src2,'/Hello2.aplf'
 (⊂'/myFirst.dll')F.NPUT src3,'/myFirst.dll'
 (⊂'/mySecond.dll')F.NPUT src3,'/mySecond.dll'
 (⊂'/LICENSE')F.NPUT src,'/LICENSE'
 cfg←TC.InitPackageConfig ⍬
 cfg.group←'Example'
 cfg.name←'Hello'
 cfg.version←'0.1.0'
 cfg.source←'TheSource/'
 cfg.assets←'Assets/'
 cfg.os_win←1
 TC.WritePackageConfigFile src cfg

 parms←TC.CreateBuildParms src
 parms.targetPath←dist
 zipfile←TC.BuildPackage parms
 →T.GoToTidyUp 0≥≢zipfile
 list←∆GetFileListFromZipArchive zipfile
 expected←{(1+≢⍵)↓¨{⍵/⍨F.IsFile ⍵}⊃('recursive' 1)F.Dir ⍵,'/'}src
 →T.GoToTidyUp~expected ∆LazyMatch list

 r←T._OK

∆TidyUp:
 (rc msg)←F.RmDirByForce dir
 msg Assert rc=0
 #.⎕EX TC.GetTatinRootSpace'#'
⍝Done
