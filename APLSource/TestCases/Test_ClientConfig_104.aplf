 r←Test_ClientConfig_104(stopFlag batchFlag);⎕TRAP;tempDir;settings;ns;list;rc;msg
⍝ Exercise both AddRegistry and ReplaceRegistry with ⎕NS''
 ⎕TRAP←(999 'C' '. ⍝ Deliberate error')(0 'N')
 r←T._Failed

 tempDir←∆GetTempDir''
 F.DeleteFile tempDir,'/',TC.UserSettings.cfg_name                           ⍝ Precaution

 settings←∆NewUserSettings(,⊂tempDir)
 →T.GoToTidyUp 2≠≢settings.registries
 ns←⎕NS''
 ns.uri←'https://localhost'

 settings.AddRegistry ns
 list←1 settings.ListRegistries ⍬
 →T.PassesIf 3=≢list
 →T.PassesIf list[;1]∆LazyMatch'tatin' 'tatin-test' ''

 ns←⎕NS''
 ns.uri←'https://localhost/'
 ns.priority←999
 ns.api_key←'xxx'
 ns.alias←'foo'
 settings.ReplaceRegistry ns
 list←1 settings.ListRegistries ⍬
 →T.PassesIf 3=≢list
 →T.PassesIf list[;1]≡'foo' 'tatin' 'tatin-test'

 r←T._OK

∆TidyUp:
 (rc msg)←F.RmDirByForce tempDir
 msg Assert rc=0
⍝Done
