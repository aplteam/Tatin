 r←Test_ClientConfig_111(stopFlag batchFlag);⎕TRAP;settings;tempDir;buff;reg;myRegDef;rc;msg
⍝ Instantiate the "UserSettings" class with a path, add registry, Save, call MakeDefaultFile and then re-instantiate
 ⎕TRAP←(999 'C' '. ⍝ Deliberate error')(0 'N')
 r←T._Failed

 tempDir←∆GetTempDir''
 F.DeleteFile tempDir,'/',TC.UserSettings.cfg_name   ⍝ Precaution

 settings←∆NewUserSettings(,⊂tempDir)
 →T.GoToTidyUp≢/F.ExpandPath¨settings.path2config(tempDir,'/',settings.cfg_name)
 settings.MakeDefaultFile 1

 buff←⊃F.NGET TC.GetUserHomeFolder'.tatin'          ⍝ Must now exist and...
 →T.GoToTidyUp tempDir{⍺≢(≢⍺)↑⍵}buff                ⍝ ... have the correct contents

 myRegDef←'[my]https://tatin.dev/'
 reg←⎕NEW TC.DefineRegistry(,⊂myRegDef)
 settings.AddRegistry reg
 settings.Save 1

 ⎕EX'settings'
 settings←⎕NEW TC.UserSettings
 →T.GoToTidyUp myRegDef≢⊃{'[',⍺,']',⍵}/(settings.GetRegistry'[my]').(alias uri)

 →T.GoToTidyUp 1≠settings.Delete 1
 →T.GoToTidyUp 0≠settings.Delete 1                  ⍝ Second time cannot be successful
 settings.Save 1

 →T.GoToTidyUp F.IsFile⊃{⍺,'/',⍵}/settings.(path2config cfg_name)

 r←T._OK

∆TidyUp:
 (rc msg)←F.RmDirByForce tempDir
 msg Assert rc=0
 F.DeleteFile TC.GetUserHomeFolder'.tatin'
⍝Done
