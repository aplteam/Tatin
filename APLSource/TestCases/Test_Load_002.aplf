 r←Test_Load_002(stopFlag batchFlag);⎕TRAP;noOf;parms
 ;dir;src;zip;body;cfg;zipfile
⍝ Build a package into a ZIP file and then load it but specify an alias
 ⎕TRAP←(999 'C' '. ⍝ Deliberate error')(0 'N')
 r←T._Failed

 #.⎕EX TC.GetTatinRootSpace'#'
 #.⎕SHADOW'TEMP' ⋄ 'TEMP'#.⎕NS'' ⍝ Used to load stuff into
 dir←∆GetTempDir ⍬
 (src zip)←(⊂dir,'/'),¨'src' 'reg'
 F.MkDir src zip

 body←':Namespace Constants' '∇r←PI' 'r←3.14' '∇' ':EndNamespace'
 (⊂body)F.NPUT src,'/Constants.apln'
 cfg←TC.InitPackageConfig ⍬
 cfg.(group name version)←'Example' 'Constants' '0.1.0'
 cfg.source←'Constants.apln'
 cfg.api←'Constants'
 cfg.os_win←1
 TC.WritePackageConfigFile src cfg
 parms←TC.CreateBuildParms src
 parms.targetPath←zip
 zipfile←TC.BuildPackage parms

 noOf←TC.LoadPackages('Foo@file://',zipfile)#.TEMP
 Assert 3=#.TEMP.Foo.⎕NC'PI'
 →T.PassesIf 3.14=#.TEMP.Foo.PI
 →T.PassesIf 0<≢#.TEMP.Foo.##.TatinVars.URI
 →T.PassesIf 0=≢#.TEMP.Foo.##.TatinVars.HOME
 →T.PassesIf 0<noOf

 r←T._OK

∆TidyUp:
 Assert 0=⊃F.RmDir dir
 #.⎕EX TC.GetTatinRootSpace'#'
⍝Done
