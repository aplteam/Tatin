 r←Test_Load_015(stopFlag batchFlag);⎕TRAP
 ;dir;reg;qdmx
⍝ Try to load a package with an undefined Registry alias
 ⎕TRAP←(999 'C' '. ⍝ Deliberate error')(0 'N')
 r←T._Failed

 #.⎕SHADOW'TEMP' ⋄ 'TEMP'#.⎕NS'' ⍝ Used to load stuff into
 dir←∆GetTempDir ⍬
 reg←dir,'/reg'
 F.MkDir reg
 TC.Reg.CreateRegistry reg
 TC.MyUserSettings.AddRegistry'[local2]file://',reg

 :Trap 998
     TC.LoadPackages'[DoesNotExist]Example-MyPackage-0.1.0'#.TEMP
     →T.GoToTidyUp 1
 :Else
     qdmx←⎕DMX
     →T.GoToTidyUp'Unknown alias'{⍺≢(≢⍺)↑⍵}qdmx.EM
 :EndTrap

 r←T._OK

∆TidyUp:
 Assert 0=⊃F.RmDir dir
 Assert TC.MyUserSettings.RemoveRegistry'[local2]'
⍝Done
