 r←Test_Load_050(stopFlag batchFlag);⎕TRAP;toBeDeleted;rc;msg;zipFilename
 ;dir;src;body;cfg;reg;list;index;expected;zipped;src2;src3;refs
⍝ Publish two packages consisting of a single function with the second depending on the first, then load them.
 ⎕TRAP←(999 'C' '. ⍝ Deliberate error')(0 'N')
 r←T._Failed

 #.⎕EX TC.GetTatinRootSpace'#'
 #.⎕SHADOW'TEMP' ⋄ 'TEMP'#.⎕NS'' ⍝ Used to load stuff into
 dir←∆GetTempDir ⍬
 (src reg)←(⊂dir,'/'),¨'src' 'reg'
 F.MkDir src reg
 F.MkDir src2←src,'/Package_A'
 toBeDeleted←''

 TC.Reg.CreateRegistry reg
 TC.MyUserSettings.AddRegistry'[local2]',reg

 body←,¨'Times←{' '  ##.Package_B.Times ⍵' '}'
 body←(⊂':Namespace Package_A'),body,(⊂':EndNamespace')
 (⊂body)F.NPUT src2,'/Package_A.apln'
 cfg←TC.InitPackageConfig ⍬
 cfg.(group name version)←'Example' 'Package_A' '1.0.0'
 cfg.source←'Package_A.apln'
 cfg.os_win←1
 cfg.api←'Package_A'
 TC.WritePackageConfigFile src2 cfg
 src2 TC.Dependencies.Add'Example-Package_B-1.0.0'
 (rc msg zipFilename)←TC.PublishPackage src2 reg
 toBeDeleted,←⊂zipFilename

 F.MkDir src3←src,'/Package_B'
 body←'r←Times y' '  r←×/ y'
 (⊂body)F.NPUT src3,'/Times.aplf'
 cfg←TC.InitPackageConfig ⍬
 cfg.(group name version)←'Example' 'Package_B' '1.0.0'
 cfg.source←'Times.aplf'
 cfg.os_win←1
 TC.WritePackageConfigFile src3 cfg
 (rc msg zipFilename)←TC.PublishPackage src3 reg
 toBeDeleted,←⊂zipFilename

 refs←TC.LoadPackages(reg,'/Example-Package_A-1.0.0')#.TEMP
 →T.GoToTidyUp 12≠#.TEMP.Package_A.Times 3 4

 r←T._OK

∆TidyUp:
 Assert 0=⊃F.RmDir dir
 Assert TC.MyUserSettings.RemoveRegistry'[local2]'
 F.DeleteFile toBeDeleted
 #.⎕EX TC.GetTatinRootSpace'#'
⍝Done
