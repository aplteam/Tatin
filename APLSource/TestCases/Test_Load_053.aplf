 r←Test_Load_053(stopFlag batchFlag);⎕TRAP;rc;msg;zipFilename;toBeDeleted
 ;dir;src;body;cfg;reg;list;index;expected;zipped;src2;src3;refs
⍝ Publish three packages two of wich depend on each other (circle), then load from specific Registry.
⍝ "A" depends on "B" which depends on "C" while "C" depends on "B".
 ⎕TRAP←(999 'C' '. ⍝ Deliberate error')(0 'N')
 r←T._Failed

 #.⎕EX TC.GetTatinRootSpace'#'
 #.⎕SHADOW'TEMP' ⋄ 'TEMP'#.⎕NS'' ⍝ Used to load stuff into
 dir←∆GetTempDir ⍬
 (src reg)←(⊂dir,'/'),¨'src' 'reg'
 F.MkDir src reg
 toBeDeleted←''

 F.MkDir src2←src,'/package_A'
 TC.Reg.CreateRegistry reg
 TC.MyUserSettings.AddRegistry'[local2]',reg

 body←':Namespace A' 'foo←{' '  ##.B.FB2 ⍵' '}' ':EndNamespace'
 (⊂body)F.NPUT src2,'/A.apln'
 cfg←TC.InitPackageConfig ⍬
 cfg.(group name version)←'Example' 'A_' '1.0.0'
 cfg.source←'A.apln'
 cfg.os_win←1
 TC.WritePackageConfigFile src2 cfg
 src2 TC.Dependencies.Add'Example-B-1.0.0'
 (rc msg zipFilename)←TC.PublishPackage src2 reg
 toBeDeleted,←⊂zipFilename
 Assert 200=rc

 F.MkDir src3←src,'/B'
 body←':Namespace B' 'FB1←{' '1+⍵' '}' 'FB2←{' '  ⍵+##.C.FC2 2' '}' ':EndNamespace'
 (⊂body)F.NPUT src3,'/B.apln'
 cfg←TC.InitPackageConfig ⍬
 cfg.(group name version)←'Example' 'B' '1.0.0'
 cfg.source←'B.apln'
 cfg.api←'B'
 cfg.os_win←1
 TC.WritePackageConfigFile src3 cfg
 src3 TC.Dependencies.Add'Example-C-1.0.0'
 (rc msg zipFilename)←TC.PublishPackage src3 reg
 toBeDeleted,←⊂zipFilename
 Assert 200=rc

 F.MkDir src3←src,'/C'
 body←':Namespace C' 'FC1←{' '⍵+3' '}' 'FC2←{' '  4+##.B.FB1 ⍵' '}' ':EndNamespace'
 (⊂body)F.NPUT src3,'/C.apln'
 cfg←TC.InitPackageConfig ⍬
 cfg.(group name version)←'Example' 'C' '1.0.0'
 cfg.source←'C.apln'
 cfg.api←'C'
 cfg.os_win←1
 TC.WritePackageConfigFile src3 cfg
 src3 TC.Dependencies.Add'Example-B-1.0.0'
 (rc msg zipFilename)←TC.PublishPackage src3 reg
 toBeDeleted,←⊂zipFilename
 Assert 200=rc

 refs←TC.LoadPackages(reg,'/Example-A_-1.0.0')#.TEMP
 →T.GoToTidyUp 37≠#.TEMP.A_.A.foo 30

 r←T._OK

∆TidyUp:
 Assert 0=⊃F.RmDir dir
 Assert TC.MyUserSettings.RemoveRegistry'[local2]'
 F.DeleteFile toBeDeleted
 #.⎕EX TC.GetTatinRootSpace'#'
⍝Done
