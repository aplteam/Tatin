 r←Test_Load_056(stopFlag batchFlag);⎕TRAP;reg1;reg2;reg3;rc;msg;zipFilename
 ;dir;src;body;cfg;list;index;expected;zipped;src2;refs
⍝ Publish a package, then load it from any Registry by specifying just the package name (no group, no version, no Registry)
 ⎕TRAP←(999 'C' '. ⍝ Deliberate error')(0 'N')
 r←T._Failed

 #.⎕EX TC.GetTatinRootSpace'#'
 #.⎕SHADOW'TEMP' ⋄ 'TEMP'#.⎕NS'' ⍝ Used to load stuff into
 dir←∆GetTempDir ⍬
 (src reg1 reg2 reg3)←(⊂dir,'/'),¨'src' 'reg1' 'reg2' 'reg3'
 F.MkDir src2←src,'/Math/Source'
 F.MkDir reg1 reg2 reg3

 TC.Reg.CreateRegistry¨reg1 reg2 reg3
 TC.MyUserSettings.AddRegistry'[lOcal2]',reg1
 TC.MyUserSettings.AddRegistry'[MycoMpany]',reg2
 TC.MyUserSettings.AddRegistry'[PriVate]',reg3

 body←':Class Math' '∇r←x Times y' ':Access Public Shared' 'r←x×y' '∇' ':EndClass'
 (⊂body)F.NPUT src2,'/Math.aplc'
 cfg←TC.InitPackageConfig ⍬
 cfg.(group name version)←'MyDepartment' 'Math' '1.0.0'
 cfg.source←'Math.aplc'
 cfg.api←'Math'
 cfg.os_win←1
 TC.WritePackageConfigFile src2 cfg
 (rc msg zipFilename)←TC.PublishPackage src2 reg2
 Assert 200=rc

 refs←TC.LoadPackages'Math'#.TEMP
 →T.GoToTidyUp 12≠3 #.TEMP.Math.Times 4

 r←T._OK

∆TidyUp:
 Assert 0=⊃F.RmDir dir
 Assert TC.MyUserSettings.RemoveRegistry'[Local2]'
 Assert TC.MyUserSettings.RemoveRegistry'[MYCOMPANy]'
 Assert TC.MyUserSettings.RemoveRegistry'[private]'
 F.DeleteFile zipFilename
 #.⎕EX TC.GetTatinRootSpace'#'
⍝Done
