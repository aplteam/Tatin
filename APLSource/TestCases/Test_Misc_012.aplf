 r←Test_Misc_012(stopFlag batchFlag);⎕TRAP;dir;src;zip;serverPath;body;cfg;packageID;rc;msg;zipFilename;list2;list1
⍝ Exercise `DeletePackage` on local Tatin server
 ⎕TRAP←(999 'C' '. ⍝ Deliberate error')(0 'N')
 r←T._Failed

 #.⎕EX TC.GetTatinRootSpace'#'
 dir←∆GetTempDir ⍬
 (src zip)←(⊂dir,'/'),¨'src' 'zip'
 F.MkDir src zip
 serverPath←'TestServer/Registry'

 body←':Namespace Test' 'foo←{' '⍵' '}' ':EndNamespace'
 (⊂body)F.NPUT src,'/Test.apln'
 cfg←TC.InitPackageConfig ⍬
 cfg.group←'test'
 cfg.name←'Package_A'
 cfg.version←'0.1.0'
 cfg.source←'Test.apln'
 cfg.os_win←1
 TC.WritePackageConfigFile src cfg

 packageID←'test-Package_A-0.1.0'
 (rc msg zipFilename)←TC.PublishPackage src'[local]'
 →T.GoToTidyUp 200≠rc
 →T.GoToTidyUp 0≠≢msg

 list1←({⍵.aggregate←0 ⋄ ⍵}⎕NS'')TC.ListPackages'[local]'
 (rc msg)←TC.DeletePackage'[local]',packageID
 Assert 200=rc
 list2←({⍵.aggregate←0 ⋄ ⍵}⎕NS'')TC.ListPackages'[local]'
 →T.GoToTidyUp'test-Package_A-0.1.0'≢∊list1[;1]~list2[;1]

 r←T._OK

∆TidyUp:
 Assert 0=⊃F.RmDir dir
 Assert 0=⊃F.RmDir serverPath,'/',packageID
 F.DeleteFile(739⌶0),'/',packageID,'.zip'
⍝Done
