 r←Test_Misc_018(stopFlag batchFlag);⎕TRAP;clientCfg;expected;tree;tree2;path;qdmx;dir;zipFiles;∆BuildDir;∆BuildSrc;∆BuildReg
⍝ Save build list on disk. Exercises `GetDependencyTree`, `SaveBuildList` and `LoadBuildList`.
 ⎕TRAP←(999 'C' '. ⍝ Deliberate error')(0 'N')
 r←T._Failed

 ∆BuildDir←'Build_Tests'∆GetTempDir''
 ∆BuildSrc←∆BuildDir,'/src/'
 ∆BuildReg←∆BuildDir,'/reg/'
 Assert F.MkDir ∆BuildSrc
 Assert F.MkDir ∆BuildReg
 Reg.CreateRegistry ∆BuildReg
 dir←'ConfigFile'∆GetTempDir''
 F.DeleteFile dir,'/',TC.UserSettings.cfg_name
 TC.InitUserSettings dir
 TC.MyUserSettings.caching←0
 TC.MyUserSettings.AddRegistry'[Build]',∆BuildReg
 zipFiles←∆CreateExampleForMinimalVersionSelection1 ∆BuildDir

 path←∆BuildReg,'minimalversion-A-1.0.0'

 tree←TC.GetDependencyTree'file://',path
 expected←'A-1.0.0' 'B-1.1.2' 'C-1.1.2' 'D-1.1.4' 'E-1.1.2'  ⍝ But not 'D-1.1.3'!
 expected←'minimalversion-'∘,¨expected
 →T.PassesIf expected ∆LazyMatch tree[;2]

 TC.SaveBuildList tree path
 tree2←TC.LoadBuildList path
 →T.PassesIf∧/tree2[;2]∊tree[;2]

 TC.Dependencies.Delete path                                  ⍝ Now we re-create the dependency file
 path TC.Dependencies.Add'minimalversion-B-1.1.2'             ⍝ That is the same as before
 path TC.Dependencies.Add'minimalversion-C-1.1.3'             ⍝ But that is now 1.1.3 rather than 1.1.2
 ⍝ Note that C-1.1.2 required D-1.1.4  while B-1.1.2 required D-1.1.3
 ⍝ C-1.1.3 does not require D at all anymore, but rather F-1.1.1
 ⍝ However, we DO NOT fall back to D-1.1.3, we stick with D-1.1.4 althoug strictly speaking that
 ⍝ version is not requested any longer by any of the packages, but we stick with the latest we have.

 tree2←TC.GetDependencyTree'file://',path
 →T.GoToTidyUp~∧/(tree[;2]~⊂'minimalversion-C-1.1.2')∊tree2[;2] ⍝ Because C-1.1.2 is neither requested nor needed

 ⍝ We compromise here: rather rebuilding everything in a second and third test case we use what we have right now
 ⎕DL 0.2
 path TC.Dependencies.Add'DoesNotExist-APLTreeUtils-6.0.1'      ⍝ Modify it: that does not exist

 #.⎕SHADOW'TEMP'
 'TEMP'#.⎕NS''
 :Trap 998
     TC.LoadDependencies path'#.TEMP'
     →T.GoToTidyUp 1
 :Else
     qdmx←⎕DMX
     →T.GoToTidyUp'Invalid entry:'{⍺≢(≢⍺)↑⍵}qdmx.EM
 :EndTrap

 r←T._OK

∆TidyUp:
 {}0 TC.InitUserSettings'./TestData'
 F.RmDir ∆BuildDir
 ⎕EX'∆BuildDir'
 F.DeleteFile zipFiles
⍝Done
