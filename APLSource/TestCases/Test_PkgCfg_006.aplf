 r←Test_PkgCfg_006(stopFlag batchFlag);⎕TRAP;dir;dist;cfg;cfg2;qdmx
 ;dir;src;zip;cfg;refs;assets
⍝ Build a package with assets that are addressed incorrectly (absolute path outside the package)
 ⎕TRAP←(999 'C' '. ⍝ Deliberate error')(0 'N')
 r←T._Failed

 #.⎕EX TC.GetTatinRootSpace'#'
 #.⎕SHADOW'TEMP' ⋄ 'TEMP'#.⎕NS'' ⍝ Used to load stuff into
 dir←∆GetTempDir ⍬
 (src zip)←(⊂dir,'/'),¨'src' 'reg'
 assets←src,'/assets'
 F.MkDir src zip
 F.MkDir assets

 (⊂'first')F.NPUT assets,'/first.dll'
 (⊂'second')F.NPUT assets,'/secnd.dll'

 cfg←TC.InitPackageConfig ⍬
 cfg.(group name version)←'Example' 'Foo' '0.1.0'
 cfg.source←'Foo.apln'
 cfg.api←'Foo'
 cfg.os_win←1
 cfg.assets←'./first.dll'
 :Trap 998
     TC.WritePackageConfigFile src cfg
     →T.GoToTidyUp 1
 :Else
     qdmx←⎕DMX
     →T.GoToTidyUp'"assets" must be addressed by a relativ path'≢qdmx.EM
 :EndTrap

 cfg.assets←dir,'/first.dll'
 :Trap 998
     TC.WritePackageConfigFile src cfg
     →T.GoToTidyUp 1
 :Else
     qdmx←⎕DMX
     →T.GoToTidyUp'"assets" must be addressed by a relativ path'≢qdmx.EM
 :EndTrap

 r←T._OK

∆TidyUp:
 Assert 0=⊃F.RmDir dir
 #.⎕EX TC.GetTatinRootSpace'#'
⍝Done
