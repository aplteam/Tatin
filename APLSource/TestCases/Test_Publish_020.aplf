 R←Test_Publish_020(stopFlag batchFlag);⎕TRAP;rc;msg;zipFilename
 ;dir;src;dist;body;cfg;reg;list;zipped;expected;list2
⍝ Publish a package with two source files in default folder ("source")
 ⎕TRAP←(999 'C' '. ⍝ Deliberate error')(0 'N')
 R←T._Failed

 #.⎕EX TC.GetTatinRootSpace'#'
 dir←∆GetTempDir ⍬
 (src dist reg)←(⊂dir,'/'),¨'src' 'dist' 'reg'
 F.MkDir src dist reg
 F.MkDir src,'/source'

 TC.Reg.CreateRegistry reg

 TC.MyUserSettings.AddRegistry'[local2]',reg

 body←':Namespace Test' 'foo←{' '⍵' '}' ':EndNamespace'
 (⊂body)F.NPUT src,'/source/Test.apln'
 body←'R←Hello' 'r←''World'''
 (⊂body)F.NPUT src,'/source/Hello.aplf'
 cfg←TC.InitPackageConfig ⍬
 cfg.group←'Example'
 cfg.name←'Package_A'
 cfg.version←'1.0.0'
 cfg.source←'source'
 cfg.os_win←1
 TC.WritePackageConfigFile src cfg

 (rc msg zipFilename)←TC.PublishPackage src'[local2]'
 Assert 200=rc

 →T.GoToTidyUp 1≠≢list←F.ListDirs reg
 →T.GoToTidyUp(,⊂'Example-Package_A-1.0.0')≢{⊃,/1↓⎕NPARTS ⍵}¨list
 →T.GoToTidyUp 2≠≢list2←F.ListFiles(1⊃list),'/'
 →T.GoToTidyUp 0=+/'/Example-Package_A-1.0.0.zip'∘{⍺≢(-≢⍺)↑⍵}¨list2
 zipped←Reg.ZipArchive.ListZipContents list2 ∆GetByExtension'.ZIP'
 expected←TC.CFG_Name'source/Test.apln' 'source/Hello.aplf'
 →T.GoToTidyUp~expected ∆LazyMatchPaths zipped

 R←T._OK

∆TidyUp:
 Assert 0=⊃F.RmDir dir
 Assert TC.MyUserSettings.RemoveRegistry'[local2]'
 F.DeleteFile zipFilename
 #.⎕EX TC.GetTatinRootSpace'#'
⍝Done
