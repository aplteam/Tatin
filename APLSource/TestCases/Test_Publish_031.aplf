 r←Test_Publish_031(stopFlag batchFlag);⎕TRAP;rc;msg;zipFilename
 ;dir;src;body;cfg;reg;list;expected;zipped;list2
⍝ Publish a simple package with two non-source files
 ⎕TRAP←(999 'C' '. ⍝ Deliberate error')(0 'N')
 r←T._Failed

 #.⎕EX TC.GetTatinRootSpace'#'
 dir←∆GetTempDir ⍬
 (src reg)←(⊂dir,'/'),¨'src' 'reg'
 F.MkDir src reg
  F.MkDir src,'/myAssets'

 TC.Reg.CreateRegistry reg
 TC.MyUserSettings.AddRegistry'[local2]',reg

 body←':Namespace Test' 'foo←{' '⍵' '}' ':EndNamespace'
 (⊂body)F.NPUT src,'/Test.apln'
 (⊂'First DLL')F.NPUT src,'/myAssets/myFirst.dll'
 (⊂'Second DLL')F.NPUT src,'/myAssets/mySecond.dll'
 cfg←TC.InitPackageConfig ⍬
 cfg.group←'Example'
 cfg.name←'Package_A'
 cfg.version←'1.0.0'
 cfg.source←'Test.apln'
 cfg.assets←'myAssets'
 cfg.os_win←1
 TC.WritePackageConfigFile src cfg

 (rc msg zipFilename)←TC.PublishPackage src reg
 Assert 200=rc

 →T.GoToTidyUp 1≠≢list←F.ListDirs reg
 →T.GoToTidyUp(,⊂'Example-Package_A-1.0.0')≢{⊃,/1↓⎕NPARTS ⍵}¨list
 →T.GoToTidyUp 2≠≢list2←F.ListFiles(1⊃list),'/'
 →T.GoToTidyUp 0=+/'/Example-Package_A-1.0.0.zip'∘{⍺≢(-≢⍺)↑⍵}¨list2
 expected←'apl-package.json' 'Test.apln' 'myAssets/myFirst.dll' 'myAssets/mySecond.dll'
 zipped←Reg.ZipArchive.ListZipContents list2 ∆GetByExtension'.ZIP'
 →T.GoToTidyUp~expected ∆LazyMatch zipped

 r←T._OK

∆TidyUp:
 Assert 0=⊃F.RmDir dir
 Assert TC.MyUserSettings.RemoveRegistry'[local2]'
 F.DeleteFile zipFilename
 #.⎕EX TC.GetTatinRootSpace'#'
⍝Done
