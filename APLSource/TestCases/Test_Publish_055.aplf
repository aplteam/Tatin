 r←Test_Publish_055(stopFlag batchFlag);⎕TRAP;dir;body;cfg;rc;msg;zipFilename;qdmx
⍝ Attempt to publish a package we have no valid API key for
 ⎕TRAP←(999 'C' '. ⍝ Deliberate error')(0 'N')
 r←T._Failed

 #.⎕EX TC.GetTatinRootSpace'#'
 dir←∆GetTempDir ⍬

 body←':Namespace A' 'foo←{' '##.Times ⍵' '}' ':EndNamespace'
 (⊂body)F.NPUT dir,'/A.apln'
 cfg←TC.InitPackageConfig ⍬
 cfg.(group name version)←'DoesNot' 'Exist' '1.0.0'
 cfg.source←'A.apln'
 cfg.os_win←1
 TC.WritePackageConfigFile dir cfg

 :Trap 998
     (rc msg zipFilename)←TC.PublishPackage dir'[tatin]'
     →T.GoToTidyUp 1
 :Else
     qdmx←⎕DMX
     →T.GoToTidyUp'Server: Unauthorized request (401)'≢qdmx.EM
 :EndTrap

 r←T._OK

∆TidyUp:
 Assert 0=⊃F.RmDir dir
 F.DeleteFile(739⌶0),'/',(⊃{⍺,'-',⍵}/cfg.(group name version)),'.zip'
⍝Done
