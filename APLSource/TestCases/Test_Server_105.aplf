 r←Test_Server_105(stopFlag batchFlag);⎕TRAP;zipFilename;rc;msg
 ;serverPath;qdmx;zip;cfg;body;clientCfg;dir;src;res
⍝ Publish a package twice - the second time must result in an error
 ⎕TRAP←(999 'C' '. ⍝ Deliberate error')(0 'N')
 r←T._Failed

 #.⎕EX TC.GetTatinRootSpace'#'
 dir←∆GetTempDir ⍬
 (src zip)←(⊂dir,'/'),¨'src' 'zip'
 F.MkDir src zip
 serverPath←∆GetServerPath

 body←':Namespace Test' 'foo←{' '⍵' '}' ':EndNamespace'
 (⊂body)F.NPUT src,'/Test.apln'
 cfg←TC.InitPackageConfig ⍬
 cfg.group←'Example'
 cfg.name←'Package_A'
 cfg.version←'0.1.0'
 cfg.source←'Test.apln'
 cfg.os_win←1
 cfg.license←'MIT'
 TC.WritePackageConfigFile src cfg

 (rc msg zipFilename)←TC.PublishPackage src(TC.ReplaceRegistryAlias '[localhost]')
 Assert 200=rc

 :Trap 998
     TC.PublishPackage src (TC.ReplaceRegistryAlias '[localhost]')
     →T.GoToTidyUp 1
 :Else
     qdmx←⎕DMX
     →T.GoToTidyUp'Server: The package has already been published'≢qdmx.EM
 :EndTrap

 r←T._OK

∆TidyUp:
 Assert 0=⊃F.RmDir dir
 Assert 0=⊃F.RmDir serverPath,'/Example-Package_A-0.1.0'
 ∆RecompileIndexOnServer
 F.DeleteFile zipFilename
⍝Done
