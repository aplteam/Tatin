 r←Test_UC_002(stopFlag batchFlag);⎕TRAP;rc;tempDir;packageFolder;zipfile;res;more;version;json;cfg;msg;statusCode;errMsg;ref
⍝ Exercise ]Tatin.PublishPackages with an already published package and different spelling (case)
⍝ We publish under group "SomeGroup" twice but with different spelling (case)
 ⎕TRAP←(999 'C' '. ⍝ Deliberate error')(0 'N')
 r←T._Failed

 tempDir←∆GetTempDir''
 F.DeleteFile tempDir,'/',TC.UserSettings.cfg_name           ⍝ Precaution

 packageFolder←F.ExpandPath ∆TEMP_TEST_DATA,'/Source/OS'
 tempDir ⎕NCOPY packageFolder,'/OS.aplc'
 tempDir ⎕NCOPY packageFolder,'/apl-package.json'
 json←⊃F.NGET tempDir,'/apl-package.json'
 cfg←⎕JSON⍠('Dialect' 'JSON5')⊢json
 cfg.group←'SomeGroup'
 cfg.version←'1.0.0'
 cfg.license←'MIT'
 json←TC.Reg.JSON cfg
 json TC.Reg.SaveJSON(tempDir,'/apl-package.json')1

 (rc msg res)←∆UCMD'PublishPackage "',tempDir,'" [localhost]'
 →T.GoToTidyUp~(rc=0)∧0=≢msg
 →T.GoToTidyUp'Package published on'{⍺≢(≢⍺)↑⍵}res

 cfg.version←'1.1.0'
 cfg.name←'os'
 json←TC.Reg.JSON cfg
 json TC.Reg.SaveJSON(tempDir,'/apl-package.json')1
 ref←∆GetRefToTatin
 ref.Client.CommTools.AddAutomation'YesOrNo' 'AlreadyPublished@' 'n'
 (rc msg res)←∆UCMD'PublishPackage "',tempDir,'" [localhost]'
 →T.GoToTidyUp~(rc=0)∧0=≢msg
 →T.GoToTidyUp'Server: The package has already been published with a different spelling (case)'≢res

 r←T._OK

∆TidyUp:
 ref.Client.CommTools.Cleanup
 Assert 0=⊃F.RmDir tempDir
 (statusCode errMsg)←TC.DeletePackages'[localhost]' 'SomeGroup-OS-1.0.0'
 Assert statusCode∧.=200
⍝Done
