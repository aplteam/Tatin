 r←Test_UC_008(stopFlag batchFlag);⎕TRAP;rc;msg;res1;res2;res3;res4;res5;res;tempDir;ns;config
⍝ Exercise ]Tatin.PackageConfig locally
 ⎕TRAP←(999 'C' '. ⍝ Deliberate error')(0 'N')
 r←T._Failed

 :If batchFlag
     →0,r←T._NoBatchTest
 :EndIf

 :If ~1 TC.YesOrNo'This test requires you to specify both "name" & "group" in a ⎕ED window; continue?'
     →0,r←T._Inactive ⍝ the user did cancel
 :EndIf

 tempDir←∆GetTempDir''
 ns←TC.InitPackageConfig ⍬
 ns.description←'This and that'
 ns.source←'source'
 ns.project_url←'https://github.com/aplteam/Tatin'
 ns.os_win←1
 0 TC.WritePackageConfigFile tempDir ns
 (rc msg res)←∆UCMD'PackageConfig ',tempDir,' -edit -quiet'
 →T.GoToTidyUp~(rc=0)∧0=≢msg
 →T.GoToTidyUp~F.IsFile tempDir,'/',Reg.CFG_Name

 (rc msg res)←∆UCMD'PackageConfig ',tempDir
 →T.GoToTidyUp~(rc=0)∧0=≢msg
 config←∆JSON∊res
 →T.GoToTidyUp 0=+/∨/¨'[JSON object]' '{JSON_object}'⍷¨⊂⍕config   ⍝ old format and 20.0-format (as of 2024-11)
 →T.GoToTidyUp~0∧.<≢¨config.(name group)

 (rc msg res)←∆UCMD'PackageConfig ',tempDir,' -delete -quiet'
 →T.GoToTidyUp~(rc=0)∧0=≢msg
 →T.GoToTidyUp 0≠F.IsFile tempDir,'/',Reg.CFG_Name

 r←T._OK

∆TidyUp:
 (rc msg)←F.RmDirByForce tempDir
 msg Assert rc=0
⍝Done
