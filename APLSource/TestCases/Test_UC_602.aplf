 r←Test_UC_602(stopFlag batchFlag);⎕TRAP;rc;msg;res;parms;ref
⍝ Exercise ]Tatin.PackageDependencies acting on an open Cider project
 ⎕TRAP←(999 'C' '. ⍝ Deliberate error')(0 'N')
 r←T._Failed

 :If 0=⎕SE.⎕NC'Cider'
     r←T._NotApplicable ⋄ →0
 :EndIf

 #.⎕SHADOW'booproj'
 parms←⎕SE.Cider.CreateOpenParms''
 parms.batch←1
 parms.folder←'TestData/Boo-Project'
 parms.ignoreUserExec←1
 parms.verbose←0
 res←⎕SE.Cider.OpenProject parms
 Assert∨/'Project successfully opened and established in: #.booproj'⍷∊res

 ref←⎕SE._Tatin.Tatin.GetRefToTatin ⍬
 ref.Client.C.AddAutomation'Select' 'SelectCiderProject@' '#.booproj'
 ref.Client.C.AddAutomation'Select' 'SelectPackageFolder@' 'packages/'
 (rc msg res)←∆UCMD'PackageDependencies'
 →T.GoToTidyUp~(rc=0)∧0=≢msg
 →T.GoToTidyUp'dyalog-HttpCommand-4.0.24'≢⎕TC~⍨∊res

 r←T._OK

∆TidyUp:
 {}0 ⎕SE.Cider.CloseProject'#.booproj'
 ref.Client.C.Cleanup
⍝Done
