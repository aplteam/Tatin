 r←Test_Uninstall_006(stopFlag batchFlag);⎕TRAP;rc
 ;dir;dir2;oldDependencies;oldBuildlist;msg;list;oldPkgList;newPkgList;newBuildlist;newDependencies
⍝ Uninstall a package that has no dependencies but just its alias
 ⎕TRAP←(999 'C' '. ⍝ Deliberate error')(0 'N')
 r←T._Failed

 dir←∆GetTempDir''
 dir ⎕NCOPY'TestData/UnInstall/'
 dir2←dir,'/UnInstall'
 oldDependencies←1⊃F.NGET(dir2,'/',TC.Reg.DependenciesFilename)1
 oldBuildlist←Reg.GetJsonFromFile dir2,'/',TC.Reg.BuildListFilename
 oldPkgList←F.ListDirs dir2,'/'

 (list msg)←TC.UnInstallPackages'EC@'dir2
 →T.GoToTidyUp~(⊂'EC@aplteam-EventCodes-3.0.1')∊list
 →T.GoToTidyUp 0≠≢msg

 newDependencies←1⊃F.NGET(dir2,'/',TC.Reg.DependenciesFilename)1
 newBuildlist←Reg.GetJsonFromFile dir2,'/',TC.Reg.BuildListFilename
 newPkgList←F.ListDirs dir2,'/'
 →T.GoToTidyUp 1≠-/≢¨oldDependencies newDependencies
 →T.GoToTidyUp 0=-/≢¨oldPkgList newPkgList
 →T.GoToTidyUp'aplteam-EventCodes-3.0.1'{(⊂⍺)∊(-≢⍺)↑⍵}1⊃oldPkgList~newPkgList

 r←T._OK

∆TidyUp:
 #.⎕EX TC.GetTatinRootSpace'#'
 (rc msg)←F.RmDirByForce dir
 msg Assert rc=0
⍝Done
