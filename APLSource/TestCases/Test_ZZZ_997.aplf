 R←Test_ZZZ_997(stopFlag batchFlag);⎕IO;⎕ML;⎕TRAP;report;buff;rf;candidate
⍝ Checks on two text vectors: " ⍝TODO⍝" and "⍝CHECK⍝"; never fails, just reports.
 ⎕IO←1 ⋄ ⎕ML←1
 ⎕TRAP←(999 'C' '. ⍝ Deliberate error')(0 'N')
 R←T._OK
 report←''
 rf←#.Tatin

 buff←rf T.FindSpecialString'⍝CHECK⍝'
 buff←(buff[;1]≢¨⎕XSI[1])⌿buff  ⍝ remove caller
 :If 0≠≢buff
     report,←↓(⊂' ⍝CHECK⍝ found:'),(⊂'   '),¨buff
 :EndIf
 buff←rf T.FindSpecialString'⍝TODO⍝'
 buff←(buff[;1]≢¨⎕XSI[1])⌿buff  ⍝ remove caller
 candidate←9=⊃∘⎕NC¨buff[;1]
 buff←(candidate{⍺:⍵≡⍕⍎⍵ ⋄ 1}¨buff[;1])⌿buff   ⍝ remove references
 :If 0≠≢buff
     report,←↓(⊂' ⍝TODO⍝ found:'),(⊂'   '),¨buff
 :EndIf
 :If 1=≢report
     report,←⊂'    Nothing found!'
 :Else
     ⎕←'Checking for ⍝TODO⍝ and ⍝CHECK⍝ '{⍵↑,'--- ',⍺,' ',⍵⍴'-'}80⌊⎕PW-1
     ⎕←↑report
 :EndIf
⍝Done
