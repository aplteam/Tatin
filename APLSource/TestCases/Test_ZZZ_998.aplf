 R←Test_ZZZ_998(stopFlag batchFlag);v;n;d;xml;rf;f1;f2;⎕TRAP;refs
⍝ Check the "Version" function.
 ⎕TRAP←(999 'C' '. ⍝ Deliberate error')(0 'N')
 R←T._Failed

 refs←#.Tatin.(Client Server)

 :For rf :In refs
     ⍝ First we check whether "Version" returns a valid result:
     (n v d)←rf.Version
     d←{⍵/⍨3>+\'-'=⍵}d      ⍝ Remove trailing stuff like "-beta"
     v←{⍵/⍨3>+\'.'=⍵}v      ⍝ Remove build ID separated by a dot (old & deprecated syntax)
     v←(¯1+v⍳'+')↑v         ⍝ New syntax: build no. is separated by a "+"
     f1←~5≤⍴v
     f1∨←2≠'.'+.=v
     f1∨←~∧/⎕D∊⍨'.'~⍨v/⍨2>+\v='.'

     f1∨←10≠⍴d
     f1∨←d[4 7+⎕IO]∨.≠'-'
     f1∨←~∧/⎕D∊⍨d~'-'

     :If f1
         ⎕←'Result of the "Version" function is invalid.'
     :EndIf

     →T.FailsIf f1
 :EndFor

 R←T._OK
