 html←type InjectCssFilenamesIntoHtml parms;css;cssFiles;cssFile;url
⍝ Inject zero, one or many CSS filenames and embrace them with a <link> tag.
 'Invalid CSS media type'⎕SIGNAL 11/⍨0=+/(⊂type)∊'screen' 'print'
 cssFiles←','A.Split parms.⍎type,'CSS'
 cssFiles←{0=+/b←'\'=r←⍵~'"':r ⋄ (b/r)←'/' ⋄ r}¨cssFiles
 html←''
 :For cssFile :In cssFiles
     url←APLTreeUtils2.DLB parms.cssURL,cssFile
     :If ~IsHTTP url
     :andif ~IsFTP url
     :andif ~IsFileProtocol url
         url←'file://',url ⍝ Because Firefox insists (2023-12-07)
     :EndIf
     html,←⊂'<link href="',url,'" rel="stylesheet" media="',type,'">'
 :EndFor
⍝Done
