 {r}←{x}CreateBranch branchName;project;folder;rc;more;buff
⍝ Creates a new branch and then checks out "master" into this new branch.\\
⍝ Note that branch names are case insensitive.\\
⍝ Overwrites the working directory, so it forces you to either commit or stage first by throwing
⍝ an error in case the working directory is dirty.\\
⍝ Blanks in `branchName` are replaced by hyphens.\\
⍝ For what `x` can actually be see `EstablishProject`.\\
 r←⍬
 x←{0<⎕NC ⍵:⍎⍵ ⋄ ''}'x'
 :If 0≠≢project←EstablishProject x
     folder←2⊃project
     'You must specify a branch name'⎕SIGNAL 6/⍨0=≢branchName
     :If IsDirty folder
     :AndIf 0=U.YesOrNo'There are uncommited changes - do you want to create the branch "',branchName,'" anyway?'
         :Return
     :EndIf
     ((' '=branchName)/branchName)←'-'
     'Branch already exists'⎕SIGNAL 11/⍨(⊂branchName)∊2↓¨1 ListBranches folder
     (rc more buff)←folder U.RunGitCommand2'checkout -b ',branchName,' master'
     :If 0=+/'Switched to a new branch'⍷∊more
         (∊more)U.Assert 0=rc
     :EndIf
     ∆TestFlag U.Show buff
     acre.RefreshProject folder
 :EndIf
⍝Done
