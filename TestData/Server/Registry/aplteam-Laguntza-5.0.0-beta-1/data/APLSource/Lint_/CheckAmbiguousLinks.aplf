 CheckAmbiguousLinks←{
     ⍺←¯1
     ∆Ref←⍵
     _←##.ShowEggTimer ∆Ref
     ∆Ref.n.infoSB.Text←'Looking for ambiguous links, please wait...'
     ∆Ref.n.infoSB.BCol←∆Ref.p.statusbarAlarmColor
     rows←⍺{¯1≡⍺:⍵ ⋄ ⍵[⍺]}∆Ref.d.tree[;##.I.Row]
     r←∆Ref∘CheckAmbiguousLinks_¨rows
     r⌿⍨←~0=≢¨r                        ⍝ Remove empty ones: they are okay
     _←∆Ref ##.SetInfoText''
     _←∆Ref ##.SetInfoBCol ¯1
     _←##.RemoveEggTimer ∆Ref
     0=≢r:0 1⍴''
     r←↑,/r
     r←(∨/¨'(ambiguous)'∘⍷¨r)/r
     0=≢r:''
     pl←1<≢r
     ⍪(⊂'*** ',(≢r),'ambiguous link',(pl/'s'),' in ',(⍕≢∪{⍵↑⍨⍵⍳':'}¨r),' topic',(pl/'s')),' ',¨r
⍝ Checks all pages (⍺ ←→ ¯1) or page(s) specified by ⍺ if ⍺ is not ¯1.
⍝ In that case ⍺ is treated as a vector of row numbers pointing into d.tree.
⍝ Returns a report that can be displayed  or analzyed by a test case.
⍝ First lines of the report is the header and starts with "*".
⍝ All following lines start with a blank.
⍝ Result is always a matrix with one column and zero to many rows.
 }
