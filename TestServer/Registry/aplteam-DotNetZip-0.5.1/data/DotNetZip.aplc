:Class DotNetZip
⍝ Zipping and unzipping with .NET Core on Windows, Linux and Mac-OS.\\
⍝ This class offers shared methods for zipping a folder and unzippping a zip file but can also
⍝ be instantiated. With `⎕NEW` either a new zip file is created or an existing one is opened,
⍝ depending on the arguments to the constructor. By default any existent zip file is overwritten.\\
⍝ You can then use the instance methods `Add`, `Delete`, `Update` and `List` as instance methods.\\
⍝ Once instanciated the read-only property carries the name of the ZIP file.\\
⍝ This class requires the class `FilesAndDirs`.\\
⍝ Kai Jaeger - APL Team Ltd.\\
⍝ Homepage: <https://github.com/aplteam/DotNetZip/>

    ⎕IO←1 ⋄ ⎕ML←1

    ∆INIT←0

    ∇ r←Version
      :Access Public Shared
      r←'DotNetZip' '0.5.1.16' '2020-05-12'
    ∇

    ∇ History
    ⍝ * 0.5.1 from 2020-05-12
    ⍝   * Minor bug fix in `ZipFolder`
    ⍝   * ...
    ⍝ * 0.5.0 from 2020-05-09
    ⍝   * First version published on GitHub as beta
    ∇

    :Property  filename
    :Access Public Instance
        ∇ r←Get
          r←_filename
        ∇
    :EndProperty

    ∇ make1(zipFilename)
    ⍝ Constructor that takes just one argument: the name of the ZIP file.\\
    ⍝ For `overwriteFlag`, the possible second argument, a default of 1 takes place.
      :Access Public Instance
      :Implements Constructor
      overwriteFlag←1
      make zipFilename overwriteFlag
    ⍝Done
    ∇

    ∇ make2(zipFilename overwriteFlag)
    ⍝ Constructor that takes two arguments:
    ⍝ * the name of the ZIP file
    ⍝ * Boolean for `overwriteFlag`
    ⍝ If `overwriteFlag` is 0 and there is already a ZIP file with that name it is not overwritten but opened.
      :Access Public Instance
      :Implements Constructor
      '"overwriteFlag" must be a Boolean'⎕SIGNAL 11/⍨~(⊂overwriteFlag)∊0 1
      make zipFilename overwriteFlag
    ⍝Done
    ∇

    ∇ make(zipFilename overwriteFlag);stream
      Initialize ⍬
      zipFilename←AddExtension F.ExpandPath zipFilename
      :If F.Exists zipFilename
      :AndIf 0=overwriteFlag
          stream←System.IO.File.Open(zipFilename System.IO.FileMode.Open)
          _zip←⎕NEW System.IO.Compression.ZipArchive(stream System.IO.Compression.ZipArchiveMode.Update)
      :Else
          F.DeleteFile zipFilename
          stream←System.IO.File.Open(zipFilename System.IO.FileMode.CreateNew)
          _zip←⎕NEW System.IO.Compression.ZipArchive(stream System.IO.Compression.ZipArchiveMode.Create)
          _zip.Dispose
          ⍝ `List` is not available in "Create" mode, so we re-open it
          stream←System.IO.File.Open(zipFilename System.IO.FileMode.Open)
          _zip←⎕NEW System.IO.Compression.ZipArchive(stream System.IO.Compression.ZipArchiveMode.Update)
      :EndIf
      _zip.⎕DF'[archive:',zipFilename,']'
      _filename←zipFilename
    ⍝Done
    ∇
⍝ ------------------------ Instance stuff

    ∇ {r}←{parent}Add toBeZipped
    ⍝ Use this to add one or more files to the ZIP file.\\
    ⍝ `toBeZipped` specifies the path (or paths) of what's going to be zipped. That's what's going to be stored in the ZIP file.
    ⍝ If `toBeZipped` refers to a full path as in `C:\Foo.txt` or `/tmp/myfile` or, much more likely, refers to
    ⍝ the current directory as in `./foo.txt` then there is no need to specify the optional left argument.\\
    ⍝ If however you want to store the file `C:\Projects\Foo\docs\ReadMe.txt` but it should be saved in the ZIP file
    ⍝ as `docs\ReadMe.txt` then you must call `Add` with this:
    ⍝ ```
    ⍝ `C:\Projects\Foo` myDotNetZip.Add `docs\ReadMe.txt`
    ⍝ ```
    ⍝ Notes:
    ⍝ * `parent` might or might not have a trailing separator, and `toBeZipped` might or might not have a
    ⍝ leading separator: `Add` works that out.
    ⍝ * While `parent`, if specified, must be a simple string, `toBeZipped` might be simple (single file) or nested, when
    ⍝ every single item is treated as a filename.\\
    ⍝ Returns `⍬` (shy).
      :Access Public Instance
      r←⍬
      parent←{0<⎕NC ⍵:⍎⍵ ⋄ ''}'parent'
      parent←F.NormalizePath{0=≢⍵:⍵ ⋄ ⍵,(~'/\'∊⍨¯1↑⍵)/F.CurrentSep}parent
      :If (≡toBeZipped)∊0 1
          toBeZipped←F.NormalizePath{⍵↓⍨'/\'∊⍨1⍴⍵}toBeZipped
          parent Add_ toBeZipped
      :Else
          toBeZipped←F.NormalizePath∘{⍵↓⍨'/\'∊⍨1⍴⍵}¨toBeZipped
          parent∘Add_¨toBeZipped
      :EndIf
    ∇

    ∇ r←List;collection
    ⍝ Lists the contents of the ZIP file
      :Access Public Instance
      collection←⌷_zip.Entries
      r←collection.ToString
    ∇

    ∇ {r}←Delete filenames;bool;toBeDeleted
    ⍝ Delete one or more files from the ZIP
    ⍝ Returns `⍬` (shy).
      :Access Public Instance
      r←⍬
      filenames←⊆F.NormalizePath filenames
      :If 0<+/bool←(⍕¨⌷_zip.Entries)∊filenames
          toBeDeleted←bool/⌷_zip.Entries
          toBeDeleted.Delete
      :EndIf
    ∇

    ∇ {r}←{parent}Update toBeZipped;entry
    ⍝ Update one ore more files within the ZIP.\\
    ⍝ `parent` is where what's going to be zipped lives. `path` specifies the full path of what's going to be zipped.
    ⍝ Therefore, `parent,path` gives the full path of the file.\\
    ⍝ For details on `parent` and `toBeZipped` see the `Add` method.
    ⍝ Returns `⍬` (shy).
      :Access Public Instance
      r←⍬
      parent←{0<⎕NC ⍵:⍎⍵ ⋄ ''}'parent'
      parent←F.NormalizePath{0=≢⍵:⍵ ⋄ ⍵,(~'/\'∊⍨¯1↑⍵)/F.CurrentSep}parent
      :If (≡toBeZipped)∊0 1
          toBeZipped←F.NormalizePath{⍵↓⍨'/\'∊⍨1⍴⍵}toBeZipped
          parent Update_ toBeZipped
      :Else
          toBeZipped←F.NormalizePath∘{⍵↓⍨'/\'∊⍨1⍴⍵}¨toBeZipped
          parent∘Update_¨toBeZipped
      :EndIf
    ∇

    ∇ {r}←Dispose
    ⍝ Use this to close the `ZipArchive` instance.\\
    ⍝ Returns `⍬` (shy).
      :Access Public Instance
      r←⍬
      :If 0<⎕NC'_zip'
          _zip.Dispose
          ⎕EX'_zip'
      :EndIf
    ∇

⍝ ------------------------ Shared stuff

    ∇ Cleanup
      :Implements Destructor
      ⍝ Calls the `Dispose` method and deleted `_zip`
      :Trap 6 90
          _zip.Dispose
      :Case 90
          Assert'Cannot access a closed file.'≡⎕EXCEPTION.Message
      :EndTrap
    ∇

    ∇ {r}←zipfile UnzipTo targetFolder
    ⍝ Unzip ⍵ into ⍺.\\
    ⍝ `targetFolder` must not yet exist.
      :Access Public Shared
      r←Initialize ⍬
      System.IO.Compression.ZipFile.ExtractToDirectory zipfile targetFolder
    ⍝Done
    ∇

    ∇ {targetFile}←sourceFolder ZipFolder targetFile
    ⍝ Takes a folder and zips it. `target` must not exist.
      :Access Public Shared
      Initialize ⍬
      targetFile←F.ExpandPath AddExtension targetFile
      sourceFolder←F.ExpandPath sourceFolder
      System.IO.Compression.ZipFile.CreateFromDirectory sourceFolder targetFile
    ⍝Done
    ∇

    ∇ r←ListZipContents zipfile;myZip
      :Access Public Shared
    ⍝ Helper that instantiates the DotNetZip class just for the purpose of unzipping a zip file
      myZip←⎕NEW ⎕THIS(zipfile 0)
      r←myZip.List
    ∇

⍝ ------------------------ Private stuff -------------------

    ∇ {r}←Initialize dummy;notwindows;netcoreset
      r←⍬
      :If 0=∆INIT
          F←(⊃⊃⎕CLASS ⎕THIS).##.FilesAndDirs
          notwindows←'Win'≢3↑⊃# ⎕WG'APLVersion'
          netcoreset←(,'1')≡2 ⎕NQ'.' 'GetEnvironment' 'DYALOG_NETCORE'
          :If notwindows∨netcoreset                      ⍝ Under Windows, switch selects .NET version
              ⎕USING←',System.IO.Compression.ZipFile' ',System.IO.Compression' ',System.IO.FileSystem' ⍝ .NET Core bridge: Lookup by name
⍝             ⎕USING←',System.IO.Compression.ZipFile'    ⍝ .NET Core bridge: Lookup by name
          :Else
              ⎕USING←',System.IO.Compression.DLL' ',System.IO.Compression.FileSystem.DLL'
          :EndIf
          ∆INIT←1
      :EndIf
    ∇

    ∇ {r}←parent Add_ toBeZipped
      r←System.IO.Compression.ZipFileExtensions.CreateEntryFromFile _zip(parent,toBeZipped)toBeZipped
    ∇

    ∇ {r}←parent Update_ toBeZipped
      Delete toBeZipped
      parent Add toBeZipped
    ∇

      AddExtension←{
      ⍝ ⍵ is a filename. If it has no extension ".zip" is added, otherwise it is left alone
          ⊃,/{(1⊃⍵),(2⊃⍵),{0=≢⍵:'.zip' ⋄ ⍵}3⊃⍵}⎕NPARTS ⍵
      }

    Assert←{⍺←'' ⋄ (,1)≡,⍵:r←1 ⋄ ⎕ML←3 ⋄ ⍺ ⎕SIGNAL 1↓(↑∊⍵),11}

:EndClass
