 ExpandAll←{
     ∆Ref←⍺
     n←∆Ref.n
     fu←⍵                                       ⍝ Force update
     ci←GetCurrentItem n                        ⍝ What's the current item?
     no←≢n.tree.SelItems                        ⍝ How many items?
     cb←n.tree.onExpanding                      ⍝ Remember callback & argument
     n.tree.onExpanding←0                       ⍝ Switch off temporarily
     states←n.tree.GetItemState¨⍳no             ⍝ Get all states
     states←⌽⍉(32⍴2)⊤states                     ⍝ convert to Booleans
     states[;5]←1                               ⍝ Expand all branches
     states←{2⊥⍉⌽⍵}states                       ⍝ Convert to Int
     n.tree.Visible←0                           ⍝ Avoid flickering by making it invisible
     _←states{n.tree.SetItemState ⍵ ⍺}¨⍳no      ⍝ Reset all states
     _←n.tree.ShowItem ci                       ⍝ Bring what was the current item back into sight
     n.tree.Visible←1                           ⍝ Bring it back
     n.tree.onExpanding←cb                      ⍝ Re-establish the callback
     _←{⍵:⎕NQ n.tree 9999 ⋄ ⍬}fu                ⍝ Force update
     _←⎕NQ n.tree'GotFocus'                     ⍝ Force focus to TreeView
     0
 ⍝ Called from the menu to collapse literally everything
 ⍝ ⍵ is boolean: 0 (default) = don't force update; 1 = force update
 }
