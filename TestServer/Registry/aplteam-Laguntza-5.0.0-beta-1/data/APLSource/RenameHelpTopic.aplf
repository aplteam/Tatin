 {r}←{testflag}RenameHelpTopic(∆Ref in);fullPageName;pageName;parent;old;newName;oldTitle;msg;rc;path;cmd
⍝ Let user (developers only) edit item number ⍵.
⍝ The user is expected to have changed the name.
⍝ If that is the case then the original one is deleted and acre is told.
⍝ This MUST NOT be called on any ∆DefaultPage.
 r←0
 testflag←{0<⎕NC ⍵:⍎⍵ ⋄ 0}'testflag'
 {}ResetStatusbar ∆Ref
 fullPageName←∆Ref GetHelpVarsName in
 pageName←1⊃'.'A.SplitPath fullPageName
 parent←⍎¯1↓↑'.'A.SplitPath fullPageName
 :If '.∆DefaultPage'{⍺≡(-≢⍺)↑⍵}pageName
     {}Show'You MUST NOT change the name of a ∆DefaultPage!'
 :Else
     old←parent.⍎pageName
     :If testflag
         newName←'A_Test'
     :Else
         newName←∆Ref AskForRename parent pageName
     :EndIf
     :If 0=≢newName
     :OrIf newName≡pageName
         {}Show'No rename performed, therefore no action taken.'
         :Return
     :EndIf
     {}testflag RenameInTopicProperties ∆Ref pageName newName parent
     ⍎(⍕parent),'.',newName,'←',fullPageName    ⍝ Copy old to new
     :If CheckOnEditFixForSaltHandler ⍬
         path←GetSaltPath parent
         cmd←'Salt.Save ',(⍕parent),'.',newName
         cmd,←' ',path,'\',newName,'.dyalog -noprompt'
         {}⎕SE.UCMD cmd
         FilesAndDirs.DeleteFile path,'\',pageName,'.dyalog'
     :Else
         :If ∆Ref.p.notifyAcre
             :If 0<⎕SE.⎕NC'acre'
                 {}⎕SE.UCMD'acre.SetChanged ',(⍕parent),'.',newName
                 {}⎕SE.UCMD'acre.Erase ',fullPageName
             :ElseIf 0<#.⎕NC'acre'
                 #.acre.SetChanged(⍕parent),'.',newName
                 :If 0=≢#.acre.Erase fullPageName
                 :AndIf ∆Ref.p{0<⍺.⎕NC ⍵:⍺⍎⍵ ⋄ 1}'reportToSession'
                     ⎕←'*** acre was told that "',pageName,'" was erased but it wasn''t interested.'
                 :EndIf
             :EndIf
         :EndIf
     :EndIf
     ⎕EX(⍕parent),'.',pageName                        ⍝ Remove old one
     oldTitle←HTML.GetTitle old
     :If 0=testflag
         {}parent EditByQED newName
         :If 0≠≢oldTitle
         :AndIf oldTitle≡HTML.GetTitle parent⍎newName
         :AndIf oldTitle≢newName
             msg←⊂'You have renamed the help page but not changed the "Title" tag.'
             msg,←'' 'If this was a mistake then don''t forget to fix it.'
             'Help: rename problem?!'Show msg
         :EndIf
     :EndIf
     {}Compile ∆Ref
     ∆Ref ShowTopic(⍕parent),'.',newName
 :EndIf
