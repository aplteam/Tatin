 {r}←{waitFlag}AllowRide port;rc;isDev;ReturnCodes
⍝ If `port` is empty or 0 no action is taken. If it is a positiv integer it is interpreted as the port number
⍝ Ride might connect to.\\
⍝ The optional left argument defaults to 1 menaing that the function will loop in order to give the user the
⍝ opportunity to issue an interrupt in order to take control.
 r←⍬
 waitFlag←{0<⎕NC ⍵:⍎⍵ ⋄ 1}'waitFlag'
 isDev←APLTreeUtils2.IsDevelopment
 ReturnCodes←⎕NS''
 ReturnCodes.CONGA_DLL_IS_MISSING←106
 ReturnCodes.RESETTING_CONNECTION_FAILED←107
 ReturnCodes.PORT_IN_USE←120
 ReturnCodes.RIDE_ERROR←109
 :If ((,0)≢,port)∧0<≢port
     :If 0=≢FilesAndDirs.Dir'conga??_',((1+∨/'-64'⍷1⊃'#'⎕WG'APLVersion')⊃'32' '64'),'.dll'
         :If isDev
             'Conga DLL is missing"'⎕SIGNAL 6
         :Else
             ⎕OFF ReturnCodes.CONGA_DLL_IS_MISSING
         :EndIf
     :Else
         {}3502⌶0
         rc←3502⌶'Serve::',⍕port
         :If rc≠0
             :If isDev
                 11 ⎕SIGNAL⍨'Ride error when resetting connection string to "',('Serve::',⍕port)'"; RC=',⍕rc
             :Else
                 ⎕OFF ReturnCodes.RESETTING_CONNECTION_FAILED
             :EndIf
         :EndIf
         rc←3502⌶1
         :If ~rc∊0 ¯1
             :If 64≠rc
                 :If isDev
                     11 ⎕SIGNAL'Another application seems to use port ',⍕port
                 :Else
                     ⎕OFF ReturnCodes.PORT_IN_USE
                 :EndIf
             :Else
                 :If isDev
                     11 ⎕SIGNAL⍨'Ride error when trying to activate Ride; RC=',⍕rc
                 :Else
                     ⎕OFF ReturnCodes.RIDE_ERROR
                 :EndIf
             :EndIf
         :EndIf
         {}{_←⎕DL ⍵ ⋄ ∇ ⍵}⍣(⊃waitFlag)⊣1 ⍝ Endless loop that allows us to have a RIDE and wait for an interrupt
     :EndIf
 :EndIf
⍝Done
