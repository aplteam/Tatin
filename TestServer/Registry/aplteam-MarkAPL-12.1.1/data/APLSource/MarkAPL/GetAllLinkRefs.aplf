 GetAllLinkRefs←{
⍝ This combs through the html and finds all link references ([][]-syntax).
⍝ Ignores inline code.
⍝ Returns a three-item-vector for each hit.
⍝ [1] The link text - that can by anything.
⍝ [2] The ref id. That's what we hope to find in ns.linkRefs later on.
⍝     This must be US ASCII letters and digits and nothing else, not even white space.
⍝ [3] The match - -everything between the opening [ and the closing ], including [].
     html←⍵
     mask←GetMaskForCodeTags¨html
     maskedHtml←mask{⎕ML←3 ⋄ 0=+/⍺:⍵ ⋄ w←⍵ ⋄ (⍺/w)←' ' ⋄ w}¨html
     hits←↓⍉↑'\[[^\]]*\]\[[^\]][A-Za-z0-9-_]*\]'⎕S(0 1 2)⊣maskedHtml
     0=≢∊hits:hits
     html∘GetLinkRef¨↓⍉↑hits
 }
