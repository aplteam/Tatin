 flag←CheckForHtmlBlock(markdown emptyLines topOfDocument);row
⍝ Returns 1 if `markdown` starts with an HTML block and 0 otherwise
 flag←0
 :If topOfDocument
 :OrIf ⊃⍴'^<pre.*>' '^<script.*>' '^<style.*>'⎕S 0⍠('Greedy' 0)⊣⊃markdown  ⍝ <pre>, <script>, <style> don't require empty lines around them.
 :OrIf ∆LastLineWasEmpty∨(⊃emptyLines)∧'<'=1↑⊃⊃1↓markdown                  ⍝ But it MAY have a blank leading line anyway!
     row←⊃(⊃emptyLines)↓markdown
     :If flag←'<'=1⍴row
     :AndIf '<<'≢2⍴row
         flag←0=≢'^<([a-zA-Z0-9]){3,6}://'⎕S 0⊣row                         ⍝ Tell implicit links (like <http://aplwiki.com>) from an HTML blocks; 3="ftp"; 6="mailto"
     :EndIf
 :EndIf
